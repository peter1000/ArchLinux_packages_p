# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Richard Stromer <noxan@byteweaver.org>
# Contributor: Carl George < arch at cgtx dot us >

_srcname=mkdocs
pkgname=python-${_srcname}-git_p
pkgver=0.14.0.r106.g3c6a66b               
pkgrel=1
pkgdesc='Project documentation with Markdown'
url="http://www.mkdocs.org"
license=('BSD')
arch=('any')
depends=('python' 
    'python-mkdocs-basic-theme'
    'python-click>=4.0' 
    'python-jinja>=2.7.1' 
    'python-livereload>=2.3.2'
    'python-markdown>=2.5'
    'python-mkdocs-bootstrap' 
    'python-mkdocs-bootswatch'
    'python-yaml>=3.10'
    'python-tornado>=4.1')
makedepends=('git' 'python-setuptools')
provides=("python-mkdocs=${pkgver}" "mkdocs=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('mkdocs' 'python-mkdocs' 'python-mkdocs-git')
source=("${_srcname}::git+https://github.com/mkdocs/${_srcname}.git")   #commit=3c6a66b  OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_srcname}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
