# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Peter Mattern <pmattern at arcor dot de>

_srcname=lxqt-sudo
pkgname=${_srcname}-git_p
pkgver=0.10.0.r1.g2b8047c               
pkgrel=1
pkgdesc="Qt front-end for sudo. Part of but not restricted to LXQt."
arch=('x86_64')
url="https://github.com/lxde/lxqt-sudo"
license=("LGPL2.1")
depends=("sudo" 'liblxqt-git_p')
provides=("lxqt-sudo=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('lxqt-sudo' 'lxqt-sudo-git')
source=("${_srcname}::git+https://github.com/lxde/${_srcname}.git")      #commit=2b8047c  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    mkdir build
    cd build
    cmake -G Ninja "${srcdir}/${_srcname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    ninja ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/build"
    DESTDIR="$pkgdir" ninja install
}
