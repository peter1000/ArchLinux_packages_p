# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Yaohan Chen <yaohan.chen@gmail.com>

_srcname=sddm-config-editor
pkgname=${_srcname}-git_p
pkgver=0.1.r21.g8cd5edc
pkgrel=1
pkgdesc='SDDM Configuration Editor'
arch=('x86_64')
url='https://github.com/hagabaka/sddm-config-editor'
license=('APACHE')
depends=('qt5-quickcontrols' 'sddm' 'polkit')
makedepends=('git')
provides=("sddm-config-editor=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('sddm-config-editor' 'sddm-config-editor-git')
source=("${_srcname}::git+https://github.com/hagabaka/${_srcname}.git")        #commit=8cd5edc  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}/cpp"
	qmake PREFIX="$pkgdir" \
        QMAKE_CFLAGS_RELEASE="${CFLAGS}"\
        QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}"
	make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/cpp"
    make DESTDIR="${pkgdir}" install
}

