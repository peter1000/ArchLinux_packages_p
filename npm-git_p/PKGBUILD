# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_srcname=npm
pkgname=${_srcname}-git_p
pkgver=3.5.3.r10.ge73b564
pkgrel=1
pkgdesc='A package manager for javascript'
arch=('any')
url='https://www.npmjs.com/'
license=('custom:Artistic')
depends=('nodejs')
makedepends=('git' 'procps-ng')
optdepends=('python2: for node-gyp')
options=('!emptydirs')
provides=("nodejs-node-gyp=${pkgver}" "npm=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('npm' 'npm-git')
source=("${_srcname}::git+https://github.com/npm/${_srcname}.git") #commit=e73b564 a81f2d2  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_srcname}"
  make NPMOPTS="--prefix=\"$pkgdir/usr\"" install

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Fix files owned by nobody:
  chown -R root "$pkgdir/usr/lib/node_modules"

  # Fix wrong symlinks
  for _dir in man1 man5 man7; do
    rm -f "$pkgdir"/usr/share/man/$_dir/*
    cd "$pkgdir"/usr/lib/node_modules/npm/man/$_dir
    for _file in *; do
      ln -s /usr/lib/node_modules/npm/man/$_dir/$_file "$pkgdir"/usr/share/man/$_dir/
    done
  done

  # Provide node-gyp executable
  cp "$pkgdir"/usr/lib/node_modules/npm/bin/node-gyp-bin/node-gyp "$pkgdir"/usr/bin/node-gyp
  sed -i 's|"`dirname "$0"`/../../|"`dirname "$0"`/../lib/node_modules/npm/|' "$pkgdir"/usr/bin/node-gyp

  install -Dm644 "$srcdir/npm/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
