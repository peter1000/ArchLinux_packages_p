# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_srcname=libqtxdg
pkgname=${_srcname}-git_p
pkgver=1.3.0.r1.g19732c6               
pkgrel=1
pkgdesc="Library providing freedesktop.org specs implementations for Qt."
arch=('x86_64')
url="http://lxqt.org"
license=("GPL2")
depends=("qt5-base")
makedepends=('git' 'cmake' 'qt5-tools' 'ninja')
provides=("libqtxdg=${pkgver}" "${pkgname}=${pkgver}")
replaces=('libqtxdg-qt5-git')
conflicts=('libqtxdg' 'libqtxdg-qt5-git')
source=("${_srcname}::git+https://github.com/lxde/${_srcname}.git")			#commit=19732c6  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    mkdir build
    cd build
    cmake -G Ninja "${srcdir}/${_srcname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    ninja ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/build"
    DESTDIR="$pkgdir" ninja install
}
