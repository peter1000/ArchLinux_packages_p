# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Ivan Shapovalov <intelfx100@gmail.com>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

_srcname=ninja
pkgname=${_srcname}-git_p
_tag=1.6.0
pkgver=1.6.0.r2134.g2eb1cc9
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=('x86_64')
url='http://martine.github.com/ninja/'
license=('Apache')
depends=('gcc-libs')
makedepends=('asciidoc' 'python' 're2c')
conflicts=('ninja' 'ninja-git')
provides=("ninja=${pkgver}" "${pkgname}=${pkgver}")
source=("${_srcname}::git://github.com/martine/${_srcname}.git")      #commit=2eb1cc9  OK
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"
    printf "${_tag}.r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"

    ./configure.py --bootstrap
    asciidoc doc/manual.asciidoc
}

check() {
    cd "${srcdir}/${_srcname}"

    python ./configure.py
    ./ninja ninja_test
    ./ninja_test --gtest_filter=-SubprocessTest.SetWithLots
}

package() {
    cd "${srcdir}/${_srcname}"

    # Main binary
    install -m755 -D ninja "${pkgdir}/usr/bin/ninja"

    # Manual
    install -m644 -D doc/manual.asciidoc "${pkgdir}/usr/share/doc/ninja/manual.asciidoc"
    install -m644 -D doc/manual.html "${pkgdir}/usr/share/doc/ninja/manual.html"

}
