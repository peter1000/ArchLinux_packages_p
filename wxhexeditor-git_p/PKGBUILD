# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Steven Honeyman <stevenhoneyman at gmail com>

_srcname=wxhexeditor
pkgname=${_srcname}-git_p
pkgver=0.23.r100.g6613dc6
pkgrel=1
pkgdesc="a free hex editor / disk editor for Linux, Windows and MacOSX"
arch=('x86_64')
url="http://wxhexeditor.sourceforge.net/"
license=('GPL')
depends=('wxgtk')
makedepends=('git')
provides=("wxhexeditor=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('wxhexeditor' 'wxhexeditor-git')
source=("${_srcname}::git+https://github.com/EUA/${_srcname}.git")    #commit=6613dc6  OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}


build() {
    export PYTHON=python2
    cd "${srcdir}/${_srcname}"
    make ${MAKEFLAGS} CXXFLAGS="${CXXFLAGS/-fno-rtti/}"
}

package() {
    export PYTHON=python2
    cd "${srcdir}/${_srcname}"
    make DESTDIR="$pkgdir" PREFIX="/usr" install
}
