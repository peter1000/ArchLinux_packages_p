# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

# NOTE: LAPACK is built into OpenBLAS by default

_srcname=OpenBLAS
pkgname=openblas-lapack-git_p
pkgver=0.2.15.r62.gb9b52c2
pkgrel=1
_provide_blas_ver=3.5.0  # the version are just the once arch currently uses for blas, lapack, cblas
arch=('x86_64')
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD (providing blas, lapack, and cblas)"
url="http://www.openblas.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
provides=("openblas=${pkgver}" "blas=${_provide_blas_ver}" "lapack=${_provide_blas_ver}"
    "cblas=${_provide_blas_ver}" 'openblas-lapack' "${pkgname}=${pkgver}")
conflicts=('openblas' 'blas' 'lapack' 'cblas' 'openblas-git' 'openblas-lapack' 'openblas-lapack-git')
source=("${_srcname}::git+https://github.com/xianyi/OpenBLAS.git")         #commit=b9b52c2  OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    make ${MAKEFLAGS} BINARY=64 USE_THREAD=1 NO_AFFINITY=1 MAJOR_VERSION=3 NO_LAPACK=0 USE_OPENMP=0 NUM_THREADS=$(nproc) libs netlib shared
}

check() {
    cd "${srcdir}/${_srcname}"
    make ${MAKEFLAGS} BINARY=64 USE_THREAD=1 NO_AFFINITY=1 MAJOR_VERSION=3 NO_LAPACK=0 USE_OPENMP=0 NUM_THREADS=$(nproc) tests
}

package() {
    cd "${srcdir}/${_srcname}"
    make BINARY=64 USE_THREAD=1 NO_AFFINITY=1 MAJOR_VERSION=3 NO_LAPACK=0 USE_OPENMP=0 NUM_THREADS=$(nproc) PREFIX=/usr DESTDIR="${pkgdir}" install

    # Install license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Symlink to provide blas, cblas and lapack
    cd "${pkgdir}/usr/lib/"
    # BLAS
    ln -sf libopenblas.so libblas.so
    ln -sf libopenblas.so libblas.so.3
    ln -sf libopenblas.so libblas.so.${_provide_blas_ver}
    # CBLAS
    ln -sf libopenblas.so libcblas.so
    # LAPACK
    ln -sf libopenblas.so liblapack.so
    ln -sf libopenblas.so liblapack.so.3
    ln -sf libopenblas.so liblapack.so.${_provide_blas_ver}

    sed -i -e "s%$pkgdir%%" "$pkgdir/usr/lib/cmake/openblas/OpenBLASConfig.cmake"
}
