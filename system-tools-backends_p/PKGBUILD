# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

_srcname=system-tools-backends
pkgname=${_srcname}_p
pkgver=2.10.2
pkgrel=5
pkgdesc='Backends for Gnome/MATE System Tools.'
arch=('x86_64')
url='http://system-tools-backends.freedesktop.org/'
license=('GPL')
depends=('dbus-glib' 'perl' 'perl-net-dbus' 'polkit')
makedepends=('intltool' 'perl-xml-parser')
provides=("system-tools-backends=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('system-tools-backends')
source=("http://ftp.gnome.org/pub/gnome/sources/${_srcname}/2.10/${_srcname}-${pkgver}.tar.bz2")
sha256sums=('1dbe5177df46a9c7250735e05e77129fe7ec04840771accfa87690111ca2c670')

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --with-dbus-sys=/etc/dbus-1/system.d \
        --localstatedir=/var \
        --mandir=/usr/share \
        --sbindir=/usr/bin \
        --disable-static
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    #clean up man
    rm -rf "${pkgdir}/usr/share/system-tools-backends-2.0/modules/share/"
}
