# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

_srcname=libebml
pkgname=${_srcname}-git_p
pkgver=1.3.3.r5.g1c93d14            
pkgrel=1
pkgdesc="Extensible Binary Meta Language library"
arch=('x86_64')
url="http://dl.matroska.org/downloads/libebml/"
license=('LGPL')
depends=('gcc-libs')
provides=("libebml=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libebml' 'libebml-git')
source=("${_srcname}::git+https://github.com/Matroska-Org/libebml.git")       #commit=81e7ffd  OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix='release-'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}
prepare() {
    cd "${srcdir}/${_srcname}"
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"
    make prefix="${pkgdir}/usr" install
}
