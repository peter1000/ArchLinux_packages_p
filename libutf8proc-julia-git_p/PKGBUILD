# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: lolilolicon #<moc.liamg@nocilolilol>.reverse

_srcname=utf8proc
pkgname=libutf8proc-julia-git_p
pkgver=1.3.1.r7.gec0daa5
pkgrel=1
pkgdesc='C library for processing UTF-8 encoded Unicode strings'
arch=('x86_64')
url='http://julialang.org/utf8proc/'
license=('custom')
depends=('glibc')
makedepends=('git')
provides=("libutf8proc=${pkgver}" "libutf8proc-julia=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('utf8proc' 'libutf8proc' 'libutf8proc-julia')
replaces=('utf8proc')
source=("${_srcname}::git+https://github.com/JuliaLang/${_srcname}.git"   #commit=ec0daa5    OK
    'libutf8proc.pc')
sha256sums=('SKIP'
            'eeb03a6a7ef2b4a448d732e8edf0b02ed4037fb60dfcfa2bbb953138a9ff41e3')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    make
}

package() {
    cd "${srcdir}/${_srcname}"
    install -Dm755 libutf8proc.so "$pkgdir/usr/lib/libutf8proc.so"
    install -Dm644 utf8proc.h "$pkgdir/usr/include/libutf8proc-julia/utf8proc.h"
    install -Dm644 ../libutf8proc.pc "$pkgdir/usr/lib/pkgconfig/libutf8proc.pc"
    install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/${pkgname}/LICENSE.md"
}
