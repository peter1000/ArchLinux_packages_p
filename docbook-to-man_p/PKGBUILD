# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Elena ``of Valhalla'' Grandi <elena.valhalla@gmail.com>

_srcname=docbook-to-man
pkgname=${_srcname}_p
pkgver=2.0.0
pkgrel=5
pkgdesc="Batch converter from DocBook SGML to nroff/troff man macros."
arch=('x86_64')
url=http://www.oasis-open.org/docbook/tools/dtm/
license=(MIT GPL3)
depends=("opensp")
provides=("docbook-to-man=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('docbook-to-man')
source=(http://www.oasis-open.org/docbook/tools/dtm/${_srcname}.tar.gz
    LICENSE
    regexp.patch)
sha512sums=('e762baf10628887eaccb3bc575f27b7555df4d8f739a201e94ebdfdf04880db82d4c4921925b64dacbeb719ed0abfd04bb06beb3493e9ee197e9e51c70fea8e5'
    '90aeb659b516ecf3e78f6389a4bb5278b88a187001f38af614eda86e58a7b573f574813f657e90bf76bf8f50bf697f656470298c685bb571ce5c29ac3c4b1bf2'
    'da1fea6d96688ebaea85e14a0b77bd3c7127c96f2bc5a8fc6ac856cd42f54117a0a7acb21ba0b7bd3f2faa32a0bbd62d47ebcb8fb6e5ca4c7fa5e22ea5eb6ae3')

prepare() {
    cd $_srcname/
    chmod +w -R .
    patch -Np1 -i ../regexp.patch
    find -name Makefile -exec sed -i 's/^ROOT =.*//' '{}' \;
    sed -ri 's:^(ROOT=).*:\1/usr:' cmd/docbook-to-man.sh
}

build() {
    make ${MAKEFLAGS} -C $_srcname ROOT="$pkgdir/usr/"
}

package() {
    install -d "$pkgdir"/usr/{bin,lib}
    make -C $_srcname ROOT="$pkgdir/usr/" install
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
