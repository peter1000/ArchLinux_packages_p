# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: SÃ©bastien Luttringer

_srcname=virtualbox-ext-oracle
pkgname=${_srcname}_p
pkgver=5.0.12
pkgrel=1
pkgdesc='Oracle VM VirtualBox Extension Pack'
arch=('any')
url='http://www.virtualbox.org/'
license=('custom:PUEL')
depends=("virtualbox=${pkgver}")
optdepends=('rdesktop: client to connect vm via RDP')
options=('!strip')
install=virtualbox-ext-oracle.install
provides=("virtualbox-ext-oracle=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('virtualbox-ext-oracle')
source=("http://download.virtualbox.org/virtualbox/${pkgver}/Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack")
noextract=("Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack")
sha512sums=('4a21cd155d1e60d3b6a4f4864cd8ca8ccd75f02448e832d72b42284dde5aa3c4aeba7f7297e3e7e198e4b4c665d770ac65c1aeca3df1dfa6a8454c47553baad7')

prepare() {
    # shrink uneeded cpuarch
    [[ -d shrunk ]] || mkdir shrunk
    tar xfC "Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack" shrunk
    rm -r shrunk/{darwin*,solaris*,win*}
    tar -c --gzip --file shrunk.vbox-extpack -C shrunk .
}

package() {
    install -Dm 644 shrunk.vbox-extpack \
        "$pkgdir/usr/share/virtualbox/extensions/Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack"
    install -Dm 644 shrunk/ExtPack-license.txt \
        "$pkgdir/usr/share/licenses/${pkgname}/PUEL"
}
