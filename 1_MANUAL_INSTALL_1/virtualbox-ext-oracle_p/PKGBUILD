# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: SÃ©bastien Luttringer

_srcname=virtualbox-ext-oracle
pkgname=${_srcname}_p
pkgver=5.0.10
pkgrel=1
pkgdesc='Oracle VM VirtualBox Extension Pack'
arch=('any')
url='http://www.virtualbox.org/'
license=('custom:PUEL')
depends=("virtualbox=${pkgver}")
optdepends=('rdesktop: client to connect vm via RDP')
options=('!strip')
install=virtualbox-ext-oracle.install
provides=("virtualbox-ext-oracle=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('virtualbox-ext-oracle')
source=("http://download.virtualbox.org/virtualbox/${pkgver}/Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack")
noextract=("Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack")
sha512sums=('ae6bab29c14b1ace849ebf5715360fc0921950c62345e18709347f44c07f88727bf1785d53437e39d67f8c56d92cef1f6e3d03c3f7d6dd76c6d4d9ee7f1f1d5b')

prepare() {
    # shrink uneeded cpuarch
    [[ -d shrunk ]] || mkdir shrunk
    tar xfC "Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack" shrunk
    rm -r shrunk/{darwin*,solaris*,win*}
    tar -c --gzip --file shrunk.vbox-extpack -C shrunk .
}

package() {
    install -Dm 644 shrunk.vbox-extpack \
        "$pkgdir/usr/share/virtualbox/extensions/Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack"
    install -Dm 644 shrunk/ExtPack-license.txt \
        "$pkgdir/usr/share/licenses/${pkgname}/PUEL"
}
