# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Richard Stromer <noxan@byteweaver.org>
# Contributor: Carl George < arch at cgtx dot us >

_srcname=mkdocs-basic-theme
pkgname=python-${_srcname}-git_p
pkgver=r7.g62b1ea3               
pkgrel=1
pkgdesc='Minimal theme for MkDocs'
url="https://github.com/mkdocs/mkdocs-basic-theme"
license=('None')
arch=('any')
depends=('python')
makedepends=('git' 'python-setuptools')
provides=("python-mkdocs-basic-theme=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('python-mkdocs-basic-theme' 'python-mkdocs-basic-theme-git')
source=("${_srcname}::git+https://github.com/mkdocs/${_srcname}.git")   #commit=62b1ea3  OK
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"
    printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_srcname}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
