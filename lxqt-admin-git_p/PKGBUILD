# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Georg Pfahler <georgpfahler@wachenzell.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Matthew Stobbs <matthew@stobbstechnical.com>

_srcname=lxqt-admin
pkgname=${_srcname}-git_p
pkgver=0.10.0.r1.g71b3e6c               
pkgrel=1
pkgdesc="LXQt system administration"
arch=('x86_64')
url="http://lxqt.org"
license=("GPL2")
depends=('liblxqt-git_p' "liboobs_p")
provides=("lxqt-admin=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('lxqt-admin' 'lxqt-admin-git')
source=("${_srcname}::git+https://github.com/lxde/${_srcname}.git")      #commit=71b3e6c  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    mkdir build
    cd build
    cmake -G Ninja "${srcdir}/${_srcname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    ninja ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/build"
    DESTDIR="$pkgdir" ninja install
}
