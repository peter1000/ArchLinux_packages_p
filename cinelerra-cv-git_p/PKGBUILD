# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

_srcname=CinelerraCV
pkgname=cinelerra-cv-git_p
pkgver=2.3.r18.g454be60
pkgrel=1
epoch=1
pkgdesc="Professional video editing and compositing environment"
arch=('x86_64')
url="http://cinelerra.org/"
license=('GPL')
depends=('e2fsprogs' 'libavc1394' 'libiec61883' 'libxv'
         'libtiff' 'mjpegtools' 'fftw' 'a52dec' 'glu'
         'faad2' 'faac' 'openexr>=2.0.0' 'hicolor-icon-theme'
         #'ffmpeg'   #454be60 got a problem using the external ffmpeg
         )
makedepends=('git' 'nasm' 'mesa')
provides=("cinelerra-cv=${pkgver}" "${_srcname}=${pkgver}")
conflicts=('cinelerra-cv' 'cinelerra-cv-git')
source=("$_srcname::git+git://git.cinelerra-cv.org/CinelerraCV.git")
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    ./autogen.sh
    
    export CFLAGS='-O3 -pipe -fomit-frame-pointer -funroll-all-loops -falign-loops=2 -ffast-math -mfpmath=sse,387 -msse'
    export CXXFLAGS="${CFLAGS}"
    
    ./configure --prefix=/usr \
        --with-buildinfo=git/recompile \
        --without-external-ffmpeg \
        --enable-opengl \
        --disable-esd
        
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"
    make DESTDIR="$pkgdir" install
}
