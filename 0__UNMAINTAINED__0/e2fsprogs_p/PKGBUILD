# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_srcname=e2fsprogs
pkgname=${_srcname}_p
pkgver=1.42.13
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64')
license=('GPL' 'LGPL' 'MIT')
url="http://e2fsprogs.sourceforge.net"
groups=('base')
depends=('sh' 'libutil-linux')
makedepends=('bc' 'util-linux')
provides=("e2fsprogs=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('e2fsprogs')
source=("http://downloads.sourceforge.net/sourceforge/${_srcname}/${_srcname}-${pkgver}.tar.gz"
        'MIT-LICENSE')
backup=('etc/mke2fs.conf')
options=('staticlibs')
install=${_srcname}.install
sha1sums=('77d1412472ac5a67f8954166ec16c37616074c37'
          'f4a0d5b0cdb980e3fedd6f5e7dde0b0ffb7bbdfb')


prepare() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in
}

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"

    ./configure --prefix=/usr --with-root-prefix="" --libdir=/usr/lib \
        --sbindir=/usr/bin --enable-elf-shlibs --disable-fsck --disable-uuidd \
        --disable-libuuid --disable-libblkid

    make ${MAKEFLAGS}
}

package() {
    unset MAKEFLAGS

    cd "${srcdir}/${_srcname}-${pkgver}"
    make DESTDIR="${pkgdir}" install install-libs

    sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

    # remove references to build directory
    sed -i -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "${pkgdir}/usr/bin/mk_cmds"
    sed -i -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "${pkgdir}/usr/bin/compile_et"

    # remove static libraries with a shared counterpart
    rm "${pkgdir}"/usr/lib/lib{com_err,e2p,ext2fs,ss}.a

    # install MIT license
    install -Dm644 "${srcdir}/MIT-LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE"
}
