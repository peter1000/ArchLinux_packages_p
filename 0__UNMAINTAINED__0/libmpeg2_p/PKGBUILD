# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_srcname=libmpeg2
pkgname=${_srcname}_p
pkgver=0.5.1
pkgrel=5
pkgdesc="Library for decoding MPEG-1 and MPEG-2 video streams."
arch=('x86_64')
url="http://libmpeg2.sourceforge.net/"
depends=('sdl' 'libxv')
source=("http://libmpeg2.sourceforge.net/files/${_srcname}-${pkgver}.tar.gz"
    'libmpeg2-0.5.1-gcc4.6.patch')
license=('GPL2')
provides=('mpeg2dec' "libmpeg2=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('mpeg2dec' 'libmpeg2')
sha256sums=('dee22e893cb5fc2b2b6ebd60b88478ab8556cb3b93f9a0d7ce8f3b61851871d4'
            '763e188eea36ee3cdfb31e7877bbead00676b5766c25175ec6a7eb20884926d1')

prepare() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    patch -Np1 -i "${srcdir}/libmpeg2-0.5.1-gcc4.6.patch"

    sed '/AC_PATH_XTRA/d' -i configure.ac
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"

    ./configure --prefix=/usr --enable-shared --disable-static
    make ${MAKEFLAGS} OPT_CFLAGS="${CFLAGS}" MPEG2DEC_CFLAGS="${CFLAGS}" LIBMPEG2_CFLAGS=""
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
