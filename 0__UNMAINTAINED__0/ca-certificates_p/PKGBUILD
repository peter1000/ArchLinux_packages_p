# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_srcname=ca-certificates
pkgbase=${_srcname}_p
pkgname=('ca-certificates-utils_p' 'ca-certificates_p')
pkgver=20150402
pkgrel=1
pkgdesc='Common CA certificates'
arch=('any')
url='http://pkgs.fedoraproject.org/cgit/ca-certificates.git'
license=('GPL2')
makedepends=('asciidoc' 'p11-kit')
source=(update-ca-trust update-ca-trust.8.txt)
sha256sums=('746d2cce8ec107fa3b7aaa246d69a7e238c3d2ac5cd82c5aeed996fe9cb0a874'
            '38c10446738c1e99bc95e42fe844a9e95ea106795059fa769f3b4ba82b395929')

build() {
	asciidoc.py -v -d manpage -b docbook update-ca-trust.8.txt
	xsltproc --nonet -o update-ca-trust.8 /etc/asciidoc/docbook-xsl/manpage.xsl update-ca-trust.8.xml
}

package_ca-certificates-utils_p() {
	pkgdesc+=" (utilities)"
	depends=('bash' 'coreutils' 'findutils' 'p11-kit>=0.23.1')
	install=ca-certificates-utils.install
	provides=("ca-certificates=${pkgver}" "ca-certificates-java=${pkgver}" "ca-certificates_p=${pkgver}" "ca-certificates-java_p=${pkgver}"
        "ca-certificates-utils=${pkgver}" "${pkgname}=${pkgver}")
	conflicts=('ca-certificates' 'ca-certificates-java' 'ca-certificates-utils')
	replaces=('ca-certificates-java')

	install -D update-ca-trust "${pkgdir}/usr/bin/update-ca-trust"
	install -Dm644 update-ca-trust.8 "${pkgdir}/usr/share/man/man8/update-ca-trust.8"

	# Trust source directories
	install -d "${pkgdir}"/{etc,usr/share}/${_srcname}/trust-source/{anchors,blacklist}

	# Directories used by update-ca-trust (aka "trust extract-compat")
	install -d "${pkgdir}"/etc/{ssl/certs/java,${_srcname}/extracted}

	# Compatibility link for OpenSSL using /etc/ssl as CAdir
	# Used in preference to the individual links in /etc/ssl/certs
	ln -s ../${_srcname}/extracted/tls-ca-bundle.pem "${pkgdir}/etc/ssl/cert.pem"
}

package_ca-certificates_p() {
	pkgdesc+=" (default providers)"
	depends=(ca-certificates-{mozilla,cacert})
}
