# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_srcname=openmpi
pkgname=${_srcname}_p
pkgver=1.10.0
pkgrel=1
pkgdesc='High performance message passing library (MPI)'
arch=('x86_64')
license=('custom')
url='http://www.open-mpi.org'
depends=('libltdl' 'hwloc')
makedepends=('inetutils' 'valgrind' 'gcc-fortran')
optdepends=('gcc-fortran: fortran support')
options=('staticlibs')
provides=("openmpi=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('openmpi')
source=(http://www.open-mpi.org/software/ompi/v${pkgver%.*}/downloads/${_srcname}-${pkgver}.tar.bz2)
sha1sums=('683fecac252a6139fbf538188d8b35454425c412')

build() {
	cd $_srcname-$pkgver

	./autogen.pl
	./configure --prefix=/usr \
			   --sysconfdir=/etc/${_srcname} \
			   --enable-mpi-fortran=all \
			   --libdir=/usr/lib/${_srcname} \
			   --with-threads=posix \
			   --enable-smp-locks \
			   --with-valgrind \
			   --enable-memchecker \
			   --enable-pretty-print-stacktrace \
			   --without-slurm \
			   --with-hwloc=/usr \
			   --with-libltdl=/usr  \
			   FC=/usr/bin/gfortran \
			   LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

	make ${MAKEFLAGS}
}

check() {
	cd $_srcname-$pkgver
	make ${MAKEFLAGS} check
}

package() {
   cd $_srcname-$pkgver
   make DESTDIR="$pkgdir" install

   # FS#28583
   install -d -m 755 "$pkgdir"/usr/lib/pkgconfig
   for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
      ln -sf /usr/lib/openmpi/pkgconfig/$i "$pkgdir"/usr/lib/pkgconfig/
   done

   # Openmpi's otfinfo conflicts with the one from texlive
   mv "$pkgdir"/usr/bin/otfinfo{,mpi}

   # Remove dangling symlink and useless file
   rm "$pkgdir"/usr/share/vampirtrace/config.log

   install -d -m 755 "$pkgdir"/etc/ld.so.conf.d
   echo "/usr/lib/$_srcname" > "$pkgdir"/etc/ld.so.conf.d/$_srcname.conf

   install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
