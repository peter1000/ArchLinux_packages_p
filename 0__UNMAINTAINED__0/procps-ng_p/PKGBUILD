# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=procps-ng
pkgname=${_srcname}_p
pkgver=3.3.11
pkgrel=2
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=('GPL' 'LGPL')
arch=('x86_64')
makedepends=('systemd')
depends=('ncurses' 'libsystemd')
provides=("procps-ng=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('procps-ng')
source=("http://downloads.sourceforge.net/project/${_srcname}/Production/${_srcname}-${pkgver}.tar.xz")
sha1sums=('1bdca65547df9ed019bd83649b0f8b8eaa017e25')

groups=('base')

conflicts=('procps' 'sysvinit-tools')
provides=('sysvinit-tools' 'procps' "procps-ng=${pkgver}" "${pkgname}=${pkgver}")
replaces=('procps' 'sysvinit-tools')

install=install

prepare() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	sed 's:<ncursesw/:<:g' -i watch.c
}

build() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--enable-watch8bit \
		--with-systemd \

	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# provided by util-linux
	rm "${pkgdir}/usr/bin/kill"
	rm "${pkgdir}/usr/share/man/man1/kill.1"
}
