# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_srcname=openssl
pkgname=${_srcname}_p
_ver=1.0.2d
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('zlib' 'perl')
optdepends=('ca-certificates')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
provides=("openssl=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('openssl')
source=("https://www.openssl.org/source/${_srcname}-${_ver}.tar.gz"
        'no-rpath.patch'
        'ca-dir.patch')
sha256sums=('671c36487785628a703374c652ad2cebea45fa920ae5681515df25d9f2c9a8c8'
            '754d6107a306311e15a1db6a1cc031b81691c8b9865e8809ac60ca6f184c957c'
            '9e8126f3a748f4c1d6fe34d4436de72b16a40e97a6d18234d2e88caa179d50c4')

prepare() {
	cd $srcdir/${_srcname}-${_ver}

	# remove rpath: http://bugs.archlinux.org/task/14367
	patch -p0 -i $srcdir/no-rpath.patch
	# set ca dir to /etc/ssl by default
	patch -p0 -i $srcdir/ca-dir.patch
}

build() {
	cd $srcdir/${_srcname}-${_ver}

	if [ "${CARCH}" == 'x86_64' ]; then
		openssltarget='linux-x86_64'
		optflags='enable-ec_nistp_64_gcc_128'
	elif [ "${CARCH}" == 'i686' ]; then
		openssltarget='linux-elf'
		optflags=''
	fi

	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
	./Configure --prefix=/usr --openssldir=/etc/ssl --libdir=lib \
		shared zlib ${optflags} \
		"${openssltarget}" \
		"-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

	make ${MAKEFLAGS} depend
	make ${MAKEFLAGS}
}

check() {
	cd $srcdir/${_srcname}-${_ver}
	# the test fails due to missing write permissions in /etc/ssl
	# revert this patch for make test
	patch -p0 -R -i $srcdir/ca-dir.patch
	make ${MAKEFLAGS} test
	patch -p0 -i $srcdir/ca-dir.patch
}

package() {
	cd $srcdir/${_srcname}-${_ver}
	make INSTALL_PREFIX=$pkgdir MANDIR=/usr/share/man MANSUFFIX=ssl install
	install -D -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}
