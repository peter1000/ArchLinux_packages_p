# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=faad2
pkgname=${_srcname}_p
pkgver=2.7
pkgrel=4
pkgdesc="ISO AAC audio decoder"
arch=('x86_64')
url="http://www.audiocoding.com/"
license=('GPL')
depends=('glibc')
options=('!makeflags')
provides=("faad2=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('faad2')
source=("http://downloads.sourceforge.net/sourceforge/faac/${_srcname}-${pkgver}.tar.bz2"
    'faad2-2.7-libmp4ff-install-mp4ff_int_types_h.patch'
    'faad2-2.7-libmp4ff-shared-lib.patch'
    'faad2-2.7-man1_MANS.patch')
sha256sums=('14561b5d6bc457e825bfd3921ae50a6648f377a9396eaf16d4b057b39a3f63b5'
            'c554313121f935aaabbe5b05addfc065bdf782bc0634ee9a91ceabde001ea823'
            'e0aaefeb20ebfec2f09e88a190f78e5744d9f1c71fe41977db8e4f29b5b88022'
            'e3e745cca309443d0149a09cc9988fec09c51e242b0e564afcb0af787dbb9031')

prepare() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    patch -p1 -i "${srcdir}/faad2-2.7-libmp4ff-shared-lib.patch"
    patch -p0 -i "${srcdir}/faad2-2.7-libmp4ff-install-mp4ff_int_types_h.patch"
    patch -p1 -i "${srcdir}/faad2-2.7-man1_MANS.patch"
    autoreconf --force --install
}

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    ./configure --prefix=/usr
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
