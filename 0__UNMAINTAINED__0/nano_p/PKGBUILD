# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd <judd@archlinux.org>

_srcname=nano
pkgname=${_srcname}_p
pkgver=2.4.2
pkgrel=2
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="http://www.nano-editor.org"
groups=('base')
depends=('ncurses' 'file' 'sh')
backup=('etc/nanorc')
install=nano.install
provides=("nano=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('nano')
source=(http://www.nano-editor.org/dist/v2.4/${_srcname}-${pkgver}.tar.gz)
sha256sums=('c8cd7f18fcf5696d9df3364ee2a840e0ab7b6bdbd22abf850bbdc951db7f65b9')

build() {
    cd ${_srcname}-${pkgver}
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --enable-color \
        --enable-nanorc \
        --enable-multibuffer \
        --disable-wrapping-as-root
    make ${MAKEFLAGS}
}

package() {
    cd ${_srcname}-${pkgver}
    make DESTDIR=${pkgdir} install
    install -DTm644 ${srcdir}/${_srcname}-${pkgver}/doc/nanorc.sample ${pkgdir}/etc/nanorc
}
