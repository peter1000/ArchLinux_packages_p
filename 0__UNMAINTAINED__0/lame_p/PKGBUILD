# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor:	Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

_srcname=lame
pkgname=${_srcname}_p
pkgver=3.99.5
pkgrel=2
pkgdesc="A high quality MPEG Audio Layer III (MP3) encoder"
arch=('x86_64')
url="http://lame.sourceforge.net/"
depends=('nasm')
license=('LGPL')
provides=("lame=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('lame' 'lame-cvs')
source=("http://downloads.sourceforge.net/$_srcname/$_srcname-$pkgver.tar.gz")
sha256sums=('24346b4158e4af3bd9f2e194bb23eb473c75fb7377011523353196b19b9a23ff')

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    ./configure --prefix=/usr \
        --enable-nasm \
        --enable-shared
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    make DESTDIR="$pkgdir" install
}
