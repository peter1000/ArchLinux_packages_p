# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=tk
pkgname=${_srcname}_p
pkgver=8.6.4
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
options=('staticlibs')
provides=("tk=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('tk')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
sha1sums=('ad24c59ac2e7453d1ed2bad0d7d18a01eabc5226')

build() {
    cd tk${pkgver}/unix
    BIT="--enable-64bit"
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --disable-rpath $BIT
    make ${MAKEFLAGS}
}

check() {
    cd tk${pkgver}/unix
    make ${MAKEFLAGS} test
}

package() {
    cd tk${pkgver}/unix
    make INSTALL_ROOT="${pkgdir}" install install-private-headers
    ln -sf wish${pkgver%.*} "${pkgdir}/usr/bin/wish"
    ln -sf libtk${pkgver%.*}.so "${pkgdir}/usr/lib/libtk.so"
    install -Dm644 license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # remove buildroot traces
    sed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib#" \
        -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
        -i "${pkgdir}/usr/lib/tkConfig.sh"
}
