# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Tom Gundersen <teg@jklm.no>

_srcname=systemd
pkgbase=${_srcname}-git_p
pkgname=('systemd-git_p' 'libsystemd-git_p' 'systemd-sysvcompat-git_p')
pkgver=227.r526.ga6bff4a
pkgrel=1
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
makedepends=('acl' 'cryptsetup' 'docbook-xsl' 'gperf' 'lz4' 'xz' 'pam'
             'intltool' 'iptables' 'kmod' 'libcap' 'libidn' 'libgcrypt'
             'libmicrohttpd' 'libxslt' 'util-linux' 'linux-api-headers'
             'python-lxml' 'quota-tools' 'shadow' 'gnu-efi-libs' 'git')
options=('strip' 'debug')
source=("${_srcname}::git+https://github.com/systemd/systemd.git#commit=a6bff4a"       #commit=c379f14 OK TAG 227
        'initcpio-hook-udev'
        'initcpio-install-systemd'
        'initcpio-install-udev'
        'arch.conf'
        'loader.conf'
        'splash-arch.bmp')
sha256sums=('SKIP'
            '5227c9bd463ae9f967a1ffbbf5f95da3c53461eb9fab97ab894fc3a2f56fe96a'
            'c0072825824c89bed83ef6883874068580296a339c85f333191b971b960fa77f'
            '0e2d052591ac85db8e831ba4c08d08dffcacfe8c2aa8a8cc25f04a4b0f67ee9a'
            'e81d73077046628ba495ed07abfc819ea270d773fe806b3ff0af527a2c6b5d42'
            '77ca6618c5e04c136eff8ddfb738a5de0742cb375edfa02b959a125024b8bf46'
            'd0ec3070db1f29799adbc8be8a26341290d2df4bf35939a5adce8e7947bad3bf')
         
pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    ./autogen.sh
}

build() {
    cd "${srcdir}/${_srcname}"

    local timeservers=({0..3}.arch.pool.ntp.org)

    ./configure \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sysconfdir=/etc \
        --enable-lz4 \
        --enable-compat-libs \
        --enable-gnuefi \
        --disable-audit \
        --disable-ima \
        --with-sysvinit-path= \
        --with-sysvrcnd-path= \
        --with-ntp-servers="${timeservers[*]}"

    make ${MAKEFLAGS}
}

package_systemd-git_p() {
    pkgdesc="system and service manager"
    license=('GPL2' 'LGPL2.1')
    depends=('acl' 'bash' 'dbus' 'iptables' 'kbd' 'kmod' 'hwids' 'libcap'
           'libgcrypt' 'libsystemd' 'libidn' 'lz4' 'pam' 'libseccomp'
           'util-linux' 'xz')

    provides=('nss-myhostname' "systemd-tools=${pkgver}" "udev=${pkgver}"
        "systemd=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('systemd' 'nss-myhostname' 'systemd-tools' 'udev')
    replaces=('nss-myhostname' 'systemd-tools' 'udev')

    optdepends=('cryptsetup: required for encrypted block devices'
              'libmicrohttpd: remote journald capabilities'
              'quota-tools: kernel-level quota management'
              'systemd-sysvcompat: symlink package to provide sysvinit binaries'
              'polkit: allow administration as unprivileged user')
    backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
          etc/dbus-1/system.d/org.freedesktop.hostname1.conf
          etc/dbus-1/system.d/org.freedesktop.login1.conf
          etc/dbus-1/system.d/org.freedesktop.locale1.conf
          etc/dbus-1/system.d/org.freedesktop.machine1.conf
          etc/dbus-1/system.d/org.freedesktop.timedate1.conf
          etc/dbus-1/system.d/org.freedesktop.import1.conf
          etc/dbus-1/system.d/org.freedesktop.network1.conf
          etc/pam.d/systemd-user
          etc/systemd/bootchart.conf
          etc/systemd/coredump.conf
          etc/systemd/journald.conf
          etc/systemd/journal-remote.conf
          etc/systemd/journal-upload.conf
          etc/systemd/logind.conf
          etc/systemd/system.conf
          etc/systemd/timesyncd.conf
          etc/systemd/resolved.conf
          etc/systemd/user.conf
          etc/udev/udev.conf)
    install="systemd.install"

    make -C "$_srcname" DESTDIR="$pkgdir" install

    # don't write units to /etc by default. some of these will be re-enabled on
    # post_install.
    rm -r "$pkgdir/etc/systemd/system/"*.wants

    # get rid of RPM macros
    rm -r "$pkgdir/usr/lib/rpm"

    # add back tmpfiles.d/legacy.conf
    install -m644 "$_srcname/tmpfiles.d/legacy.conf" "$pkgdir/usr/lib/tmpfiles.d"

    # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
    sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules
    sed -i 's/dialout/uucp/g;
          s/tape/storage/g;
          s/cdrom/optical/g' "$pkgdir"/usr/lib/sysusers.d/basic.conf

    # add mkinitcpio hooks
    install -Dm644 "$srcdir/initcpio-install-systemd" "$pkgdir/usr/lib/initcpio/install/systemd"
    install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
    install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"

    # ensure proper permissions for /var/log/journal. This is only to placate
    chown root:systemd-journal "$pkgdir/var/log/journal"
    chmod 2755 "$pkgdir/var/log/journal"

    # we'll create this on installation
    rmdir "$pkgdir/var/log/journal/remote"

    # fix pam file
    sed 's|system-auth|system-login|g' -i "$pkgdir/etc/pam.d/systemd-user"

    # ship default policy to leave services disabled
    echo 'disable *' >"$pkgdir"/usr/lib/systemd/system-preset/99-default.preset

    ### manpages shipped with systemd-sysvcompat
    rm "$pkgdir"/usr/share/man/man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8

    ### runtime libraries shipped with libsystemd
    rm "$pkgdir"/usr/lib/lib{nss,systemd,udev}*.so*

    # add example bootctl configuration
    install -Dm644 "$srcdir/arch.conf" "$pkgdir"/usr/share/systemd/bootctl/arch.conf
    install -Dm644 "$srcdir/loader.conf" "$pkgdir"/usr/share/systemd/bootctl/loader.conf
    install -Dm644 "$srcdir/splash-arch.bmp" "$pkgdir"/usr/share/systemd/bootctl/splash-arch.bmp
}

package_libsystemd-git_p() {
    pkgdesc="systemd client libraries"
    depends=('glibc' 'libgcrypt' 'lz4' 'xz')
    license=('GPL2')
    provides=('libsystemd.so' 'libsystemd-daemon.so' 'libsystemd-id128.so'
            'libsystemd-journal.so' 'libsystemd-login.so' 'libudev.so'
            "libsystemd=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('libsystemd')

    make -C "$_srcname" DESTDIR="$pkgdir" install-libLTLIBRARIES
}

package_systemd-sysvcompat-git_p() {
    pkgdesc="sysvinit compat for systemd"
    license=('GPL2')
    groups=('base')
    depends=('systemd')
    provides=("sysvinit=${pkgver}" "sysvinit-git_p=${pkgver}" "systemd-sysvcompat=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('sysvinit' 'systemd-sysvcompat' 'systemd-sysvcompat-git')

    install -dm755 "$pkgdir"/usr/share/man/man8
    cp -d --no-preserve=ownership,timestamp \
        "$_srcname"/man/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8 \
        "$pkgdir"/usr/share/man/man8

    install -dm755 "$pkgdir/usr/bin"
    for tool in runlevel reboot shutdown poweroff halt telinit; do
        ln -s 'systemctl' "$pkgdir/usr/bin/$tool"
    done

    ln -s '../lib/systemd/systemd' "$pkgdir/usr/bin/init"
}
