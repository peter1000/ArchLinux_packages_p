# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

_srcname=pulseaudio
pkgbase=${_srcname}_p
pkgname=(pulseaudio_p libpulse_p pulseaudio-{gconf_p,zeroconf_p,lirc_p,xen_p,jack_p,bluetooth_p,equalizer_p})
pkgdesc="A featureful, general-purpose sound server"
pkgver=7.0
pkgrel=2
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/PulseAudio"
license=(LGPL)
makedepends=('libasyncns' 'libcap' 'attr' 'libxtst' 'libsm' 'libsndfile' 'libtool rtkit' 'libsoxr'
    'speexdsp' 'tdb' 'systemd' 'dbus' 'avahi' 'bluez' 'bluez-libs' 'gconf' 'intltool' 'jack2' 'sbc'
    'lirc' 'openssl' 'xenstore' 'fftw' 'orc' 'json-c' 'gtk3' 'webrtc-audio-processing'
    'check')
options=('!emptydirs')
source=("http://freedesktop.org/software/pulseaudio/releases/${_srcname}-${pkgver}.tar.xz"
    'padsp-lib32.patch')
sha256sums=('ca1ae1377e8926bfc3ffe2aeb9f657f6c363a16f72861166fcf9454e3eeae8fa'
            '7832fc59df76538ff10aedd297c03cb7ff117235da8bfad26082994bb5b84332')
        
build() {
    cd "${_srcname}-${pkgver}"

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --with-udev-rules-dir=/usr/lib/udev/rules.d \
        --with-database=tdb \
        --disable-tcpwrap \
        --disable-bluez4 \
        --disable-samplerate \
        --disable-rpath \
        --disable-default-build-tests

    # fight unused direct deps
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make ${MAKEFLAGS}
}

package_pulseaudio_p() {
    depends=("libpulse_p=$pkgver-$pkgrel" 'rtkit' 'libltdl' 'speexdsp' 'tdb' 'orc' 'webrtc-audio-processing'
        'sbc' 'libsoxr')
    optdepends=('pulseaudio-alsa: ALSA configuration (recommended)')
    backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
    install=pulseaudio.install
    provides=("pulseaudio=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio')

    cd "${_srcname}-${pkgver}"
    
    _pulsever=$(./git-version-gen .tarball-version)
    while [[ $_pulsever = *.*.* ]]; do
        _pulsever=${_pulsever%.*}
    done
    _pulsever=${_pulsever%%-*}
    
    make -j1 DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions

    cd "$pkgdir"
    patch -Np1 -i "$srcdir/padsp-lib32.patch"

    sed -e '/flat-volumes/iflat-volumes=no' -i etc/pulse/daemon.conf

    # Superseded by socket activation
    sed -e '/autospawn/iautospawn=no' -i etc/pulse/client.conf

    # Disable cork-request module, can result in e.g. media players unpausing
    # when there's a Skype call incoming
    sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' -i usr/bin/start-pulseaudio-x11

    rm etc/dbus-1/system.d/pulseaudio-system.conf

    mkdir usr/lib/systemd/user/sockets.target.wants
    ln -s ../pulseaudio.socket usr/lib/systemd/user/sockets.target.wants/pulseaudio.socket

    ### Split libpulse

    mkdir -p "$srcdir"/libpulse/{etc/pulse,usr/{bin,lib/pulseaudio,share/man/man{1,5}}}

    mv etc/pulse/client.conf "$srcdir/libpulse/etc/pulse"

    mv usr/bin/pa{cat,ctl,dsp,mon,play,rec,record} "$srcdir/libpulse/usr/bin"

    mv usr/lib/libpulse{,-simple,-mainloop-glib}.so* usr/lib/{cmake,pkgconfig} "$srcdir/libpulse/usr/lib"

    mv usr/lib/pulseaudio/libpulse{dsp,common-*}.so "$srcdir/libpulse/usr/lib/pulseaudio"

    mv usr/include "$srcdir/libpulse/usr"

    mv usr/share/man/man1/pa{cat,ctl,dsp,mon,play,rec,record}.1 "$srcdir/libpulse/usr/share/man/man1"

    mv usr/share/man/man5/pulse-client.conf.5 "$srcdir/libpulse/usr/share/man/man5"

    mv usr/share/vala "$srcdir/libpulse/usr/share"

    ### Split modules

    mkdir -p "$srcdir"/{gconf,zeroconf,lirc,xen,jack,bluetooth,equalizer}/usr/lib/pulse-$_pulsever/modules \
           "$srcdir"/{gconf/usr/lib/pulse,equalizer/usr/bin}

    mv usr/lib/pulse-$_pulsever/modules/module-gconf.so "$srcdir/gconf/usr/lib/pulse-$_pulsever/modules"
    mv usr/lib/pulse/gconf-helper "$srcdir/gconf/usr/lib/pulse"

    mv usr/lib/pulse-$_pulsever/modules/{libavahi-wrap,module-{zeroconf-{publish,discover},raop-discover}}.so \
     "$srcdir/zeroconf/usr/lib/pulse-$_pulsever/modules"

    mv usr/lib/pulse-$_pulsever/modules/module-lirc.so "$srcdir/lirc/usr/lib/pulse-$_pulsever/modules"

    mv usr/lib/pulse-$_pulsever/modules/module-xenpv-sink.so "$srcdir/xen/usr/lib/pulse-$_pulsever/modules"

    mv usr/lib/pulse-$_pulsever/modules/module-jack{-sink,-source,dbus-detect}.so "$srcdir/jack/usr/lib/pulse-$_pulsever/modules"

    mv usr/lib/pulse-$_pulsever/modules/{libbluez5-util,module-{bluetooth-{discover,policy},bluez5-{discover,device}}}.so \
     "$srcdir/bluetooth/usr/lib/pulse-$_pulsever/modules"

    mv usr/lib/pulse-$_pulsever/modules/module-equalizer-sink.so "$srcdir/equalizer/usr/lib/pulse-$_pulsever/modules"
    mv usr/bin/qpaeq "$srcdir/equalizer/usr/bin"
}

package_libpulse_p() {
    pkgdesc="$pkgdesc (client library)"
    depends=(dbus libasyncns libcap libxtst libsm libsndfile-git_p json-c systemd)
    backup=(etc/pulse/client.conf)
    provides=("libpulse=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('libpulse')

    mv "$srcdir"/libpulse/* "$pkgdir"
}

package_pulseaudio-gconf_p(){
    pkgdesc="GConf support for PulseAudio"
    depends=(pulseaudio_p gconf)
    provides=("pulseaudio-gconf=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio-gconf')
    mv "$srcdir"/gconf/* "$pkgdir"
}

package_pulseaudio-zeroconf_p(){
    pkgdesc="Zeroconf support for PulseAudio"
    depends=(pulseaudio_p avahi openssl)
    provides=("pulseaudio-zeroconf=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio-zeroconf')
    mv "$srcdir"/zeroconf/* "$pkgdir"
}

package_pulseaudio-lirc_p(){
    pkgdesc="IR (lirc) support for PulseAudio"
    depends=(pulseaudio_p lirc)
    provides=("pulseaudio-lirc=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio-lirc')

    mv "$srcdir"/lirc/* "$pkgdir"
}

package_pulseaudio-xen_p(){
    pkgdesc="Xen support for PulseAudio"
    depends=(pulseaudio_p xenstore)
    license=(GPL2)
    provides=("pulseaudio-xen=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio-xen')
    mv "$srcdir"/xen/* "$pkgdir"
}

package_pulseaudio-jack_p(){
    pkgdesc="Jack support for PulseAudio"
    depends=(pulseaudio jack2)
    provides=("pulseaudio-jack=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio-jack')
    mv "$srcdir"/jack/* "$pkgdir"
}

package_pulseaudio-bluetooth_p(){
    pkgdesc="Bluetooth support for PulseAudio"
    depends=(pulseaudio bluez bluez-libs)
    provides=("pulseaudio-bluetooth=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio-bluetooth')
    mv "$srcdir"/bluetooth/* "$pkgdir"
}

package_pulseaudio-equalizer_p(){
    pkgdesc="Equalizer for PulseAudio"
    depends=('pulseaudio' 'python-pyqt4' 'fftw')
    license=(AGPL3)
    provides=("pulseaudio-equalizer=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('pulseaudio-equalizer')
    mv "$srcdir"/equalizer/* "$pkgdir"
}
