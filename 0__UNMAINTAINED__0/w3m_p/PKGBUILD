# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

_srcname=w3m
pkgname=${_srcname}_p
pkgver=0.5.3
pkgrel=10
pkgdesc='Text-based Web browser, as well as pager'
url='http://w3m.sourceforge.net/'
license=('custom')
arch=('x86_64')
depends=('openssl' 'gc' 'ncurses' 'imlib2')
makedepends=('imlib2')
provides=("w3m=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('w3m')
source=("http://downloads.sourceforge.net/project/${_srcname}/${_srcname}/${_srcname}-${pkgver}/${_srcname}-${pkgver}.tar.gz"
        'form_unknown.patch'
        'file_handle.patch'
        'https.patch'
        'color.patch'
        'perl.patch'
        'gc72.patch')
sha1sums=('444b6c8cf7094ee95f8e9de96b37f814b9d83237'
          '23d648033ad77f3c67d370c62cf7b7ed86b5c6f2'
          '457de43be7eabc652614461a7ab25e3ba76b2aff'
          '66affb2f695fe0bdde25cf774642bfd6a9404e88'
          '9c30038c7672508a93e55044248b29ed572cc13e'
          'a8fb1f738688104ad8f0713bd981083e12e878ad'
          '7baa06ca684fd140503a2adc65e2d606970c4662')

prepare() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	ls ../*.patch | xargs -i patch -p1 -i {}
}

build() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	export LIBS="-lX11"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \
		--disable-mouse \

	sed 's/@my_libs@/-lX11/g' -i Makefile
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${_srcname}"
	find doc/* | grep -v CVS | xargs -i install -m644 "{}" "${pkgdir}/usr/share/doc/${_srcname}"
	ln -s ../../doc/"${_srcname}"/README "${pkgdir}/usr/share/licenses/${pkgname}"
}
