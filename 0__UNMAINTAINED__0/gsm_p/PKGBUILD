# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

_srcname=gsm
pkgname=${_srcname}_p
pkgver=1.0.14
pkgrel=1
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
conflicts=('gsm' 'gsm-git')
provides=("gsm=${pkgver}" "${pkgname}=${pkgver}")
source=("http://www.quut.com/gsm/gsm-${pkgver}.tar.gz"
    'gsm.patch')
sha256sums=('5814a16a30b3c026871b3739812dc4a2a84299331182c987da1c212c93e9352c'
    'f2883e05bfe7faa6298c61bd68fbf99fed6b5e1024267547667ca859d33b74a1')

prepare() {
    cd "${srcdir}/${_srcname}-${pkgver%.*}-pl${pkgver##*.}/"
    patch -Np0 -i "${srcdir}/${_srcname}.patch"
}

build() {
    cd "${srcdir}/${_srcname}-${pkgver%.*}-pl${pkgver##*.}/"
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver%.*}-pl${pkgver##*.}/"

    # Prepare directories
    install -m755 -d "${pkgdir}"/usr/{bin,lib,include/gsm,share/{licenses/${pkgname},man/man{1,3}}}

    make -j1 INSTALL_ROOT="${pkgdir}/usr" \
        GSM_INSTALL_INC="${pkgdir}/usr/include/gsm" \
        GSM_INSTALL_MAN="${pkgdir}/usr/share/man/man3" \
        TOAST_INSTALL_MAN="${pkgdir}/usr/share/man/man1" \
        install

    # Install license
    install -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
