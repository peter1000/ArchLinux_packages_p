# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=tcl
pkgname=${_srcname}_p
pkgver=8.6.4
pkgrel=1
pkgdesc="The Tcl scripting language"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('zlib')
options=('staticlibs')
provides=("tcl=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('tcl')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
sha1sums=('33c5a083a23cf54f15e25a9e787dc39ef3fa0a2b')

prepare() {
	cd tcl${pkgver}
	# we build the tcl sqlite interface in sqlite-tcl package
	rm -rf pkgs/sqlite3*
}

build() {
	cd tcl${pkgver}/unix
	BIT="--enable-64bit"
	./configure --prefix=/usr --mandir=/usr/share/man --enable-threads $BIT
	make ${MAKEFLAGS}
}

check() {
	cd tcl${pkgver}/unix
	make ${MAKEFLAGS} test
}

package() {
	cd tcl${pkgver}/unix
	make INSTALL_ROOT="${pkgdir}" install install-private-headers
	ln -sf tclsh${pkgver%.*} "${pkgdir}/usr/bin/tclsh"
	ln -sf libtcl${pkgver%.*}.so "${pkgdir}/usr/lib/libtcl.so"
	install -Dm644 ../license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# remove buildroot traces
	sed -e "s#${srcdir}/tcl${pkgver}/unix#/usr/lib#" \
		-e "s#${srcdir}/tcl${pkgver}#/usr/include#" \
		-i "${pkgdir}/usr/lib/tclConfig.sh"

	tdbcver=tdbc1.0.3
	sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$tdbcver#/usr/lib/$tdbcver#" \
		-e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/generic#/usr/include#" \
		-e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/library#/usr/lib/tcl${pkgver%.*}#" \
		-e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver#/usr/include#" \
		-i "${pkgdir}/usr/lib/$tdbcver/tdbcConfig.sh"

	itclver=itcl4.0.3
	sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$itclver#/usr/lib/$itclver#" \
		-e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver/generic#/usr/include#" \
		-e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver#/usr/include#" \
		-i "${pkgdir}/usr/lib/$itclver/itclConfig.sh"
}
