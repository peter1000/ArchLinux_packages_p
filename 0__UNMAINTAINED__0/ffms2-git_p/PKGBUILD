# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

_srcname=ffms2
pkgname=${_srcname}-git_p
pkgver=2.22.r0.g129952a         #commit=129952a  OK
pkgrel=1
pkgdesc="An FFmpeg/Libav based source library and Avisynth/Vapoursynth plugin for easy frame accurate access (GIT version)"
url="https://github.com/FFMS/ffms2"
arch=('x86_64')
license=('MIT')
depends=('ffmpeg')
makedepends=('git')
provides=('vapoursynth-plugin-ffms2-git', 'libffms2.so' "ffms2=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('ffms2' 'ffms2-git' 'vapoursynth-plugin-ffms2-git')
replaces=('ffmpegsource')
source=("${_srcname}::git+https://github.com/FFMS/${_srcname}.git#commit=129952a")
sha256sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr --enable-shared=yes --enable-static=no --enable-avresample
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"
    make DESTDIR="${pkgdir}" install
    install -d "${pkgdir}/usr/lib/vapoursynth"
    ln -s /usr/lib/libffms2.so "${pkgdir}/usr/lib/vapoursynth/libffms2.so"
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
