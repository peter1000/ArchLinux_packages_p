# Maintainer: peter1000 <https://github.com/peter1000>

_srcname=PSphinxTheme
pkgname=python-psphinxtheme-git_p
pkgver=2.0.5.r0.g0709048               #commit=xxxx  OK
pkgrel=1
arch=(any)
pkgdesc="Python P-SphinxTheme."
url='https://github.com/peter1000/PSphinxTheme'
license=(BSD)
depends=('python' 'python-sphinx>=1.3.1') # python-lconf-lexer-git_p>=1.0.1
makedepends=('git' 'python-setuptools')
provides=("python-psphinxtheme=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('python-psphinxtheme' 'python-psphinxtheme-git')
source=("${_srcname}::git+https://github.com/peter1000/${_srcname}.git")
sha256sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_srcname}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.rst"
}
