# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>

_srcname=cracklib
pkgname=${_srcname}_p
pkgver=2.9.4
pkgrel=1
pkgdesc="Password Checking Library"
arch=('x86_64')
license=('GPL')
url="http://sourceforge.net/projects/cracklib"
depends=('glibc' 'zlib')
provides=("cracklib=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('cracklib')
source=(http://downloads.sourceforge.net/sourceforge/$_srcname/$_srcname-$pkgver.tar.gz)
sha1sums=('80af5eb9a5e6bf37b549d44f2d2288a85c4922bb')

build() {
    cd $srcdir/$_srcname-$pkgver
    ./configure --prefix=/usr --sbindir=/usr/bin --without-python
    make ${MAKEFLAGS} 
}

check() {
    cd $srcdir/$_srcname-$pkgver
    make ${MAKEFLAGS} check
}

package() {
    cd $srcdir/$_srcname-$pkgver
    make DESTDIR=$pkgdir install
    # symlink cracklib-small #34778
    mkdir -p $pkgdir/usr/share/dict
    ln -sf /usr/share/cracklib/cracklib-small $pkgdir/usr/share/dict/cracklib-small
    sh ./util/cracklib-format dicts/cracklib-small \
        | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}
