# Maintainer: peter1000 <https://github.com/peter1000>

_srcname=gettext
pkgname=${_srcname}_p
pkgver=0.19.6
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('x86_64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
groups=('base' 'base-devel')
depends=('gcc-libs' 'acl' 'sh' 'glib2' 'libunistring')
optdepends=('git: for autopoint infrastructure updates')
options=(!docs)
install=gettext.install
provides=("gettext=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('gettext')
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_srcname}-${pkgver}.tar.gz)
sha256sums=('ed4b4c19bd3a3034eb6769500a3592ff616759ef43cf30586dbb7a17c9dd695d')

build() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	./configure --prefix=/usr
	make ${MAKEFLAGS}
}

check() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	make ${MAKEFLAGS} -k check
}

package() {
	cd "${srcdir}/${_srcname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

