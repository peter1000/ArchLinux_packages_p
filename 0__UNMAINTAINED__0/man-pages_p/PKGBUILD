# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor:  Andreas Radke <andyrtr@archlinux.org>

_srcname=man-pages
pkgname=${_srcname}_p
pkgver=4.02
_posixver=2013-a
pkgrel=1
pkgdesc="Linux man pages"
arch=('any')
license=('GPL' 'custom')
url="http://man7.org/linux/man-pages/index.html"
groups=('base')
depends=()
provides=("man-pages=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('man-pages')
source=(http://www.kernel.org/pub/linux/docs/man-pages/$_srcname-$pkgver.tar.xz
        http://www.kernel.org/pub/linux/docs/man-pages/man-pages-posix/$_srcname-posix-${_posixver}.tar.xz)
sha256sums=('48aacb75d522dd31978682c4fd8bc68e43c9a409bc4c7a126810e7610dff0dd3'
            '19633a5c75ff7deab35b1d2c3d5b7748e7bd4ef4ab598b647bb7e7f60b90a808')

build() {
    cd ${srcdir}/$_srcname-$pkgver

    # move the posix pages
    mkdir -p ${srcdir}/$_srcname-$pkgver/man0
    for sect in 0 1 3; do
        sed -i "/^\.so /s/man${sect}p/man$sect/" "${srcdir}/$_srcname-posix-${_posixver}/man${sect}p"/*
        mv -iv "${srcdir}/$_srcname-posix-${_posixver}/man${sect}p"/* "${srcdir}/$_srcname-$pkgver/man$sect/"
    done
}

package() {
    cd ${srcdir}/$_srcname-$pkgver

    make prefix=${pkgdir}/usr install

    # posix pages have a custom license
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 ${srcdir}/$_srcname-posix-${_posixver}/POSIX-COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/POSIX-COPYRIGHT"

    cd ${pkgdir}/usr/share/man
    # these are included in coreutils
    rm -f man1/{chgrp,chmod,chown,cp,dir,dd}.1
    rm -f man1/{df,dircolors,du,install,ln,ls}.1
    rm -f man1/{mkdir,mkfifo,mknod,mv,rm,rmdir}.1
    rm -f man1/{touch,vdir}.1
    # this is included in shadow
    rm -f man5/passwd.5
    rm -f man3/getspnam.3
    # this is included in diffutils
    rm -f man1/diff.1
    # this is included in xf86-input-mouse
    rm -f man4/mouse.4
    # this is included in attr
    rm -f man5/attr.5  
}
