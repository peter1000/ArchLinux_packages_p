# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Allan McRae <allan@archlinux.org>

_srcname=libmpc
pkgname=${_srcname}_p
pkgver=1.0.3
pkgrel=1
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"
arch=('x86_64')
url="http://www.multiprecision.org/"
license=('LGPL')
depends=('mpfr')
install=libmpc.install
provides=("libmpc=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libmpc')
source=(http://www.multiprecision.org/mpc/download/mpc-${pkgver/_/-}.tar.gz)
sha256sums=('617decc6ea09889fb08ede330917a00b16809b8db88c29c31bfbb49cbf88ecc3')

build() {
    cd "${srcdir}/mpc-${pkgver}"
    ./configure --prefix=/usr
    make ${MAKEFLAGS}
}

check() {
    cd "${srcdir}/mpc-${pkgver}"
    make ${MAKEFLAGS} check
}

package() {
    cd "${srcdir}/mpc-${pkgver}"
    make DESTDIR="${pkgdir}" install
    mv ${pkgdir}/usr/share/info/{mpc,libmpc}.info
}
