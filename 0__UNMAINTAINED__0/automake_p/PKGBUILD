# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_srcname=automake
pkgname=${_srcname}_p
pkgver=1.15
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'python' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope')
install=automake.install
provides=("automake=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('automake')
source=(ftp://ftp.gnu.org/gnu/${_srcname}/${_srcname}-${pkgver}.tar.xz)
sha256sums=('9908c75aabd49d13661d6dcb1bc382252d22cc77bf733a2d55e87f2aa2db8636')

prepare() {
	cd ${srcdir}/${_srcname}-${pkgver}

	# skip failing test...
	sed -i "/instmany-python.sh/d" Makefile.in
}

build() {
	cd ${srcdir}/${_srcname}-${pkgver}
	./configure --prefix=/usr
	make ${MAKEFLAGS}
}

check() {
	cd ${srcdir}/${_srcname}-${pkgver}
	make ${MAKEFLAGS} check
}

package() {
	cd ${srcdir}/${_srcname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
