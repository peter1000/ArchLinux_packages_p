# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

_srcname=linux-api-headers
pkgname=${_srcname}_p
pkgver=4.3.0
_basever=4.3
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL2')
provides=("linux-api-headers=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('linux-api-headers')
source=(http://www.kernel.org/pub/linux/kernel/v4.x/linux-${_basever}.tar.xz
        #http://www.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz
        )
sha256sums=('4a622cc84b8a3c38d39bc17195b0c064d2b46945dfde0dae18f77b120bc9f3ae')
            
prepare() {
    cd ${srcdir}/linux-${_basever}
    [[ $pkgver != $_basever ]] && patch -p1 -i ${srcdir}/patch-${pkgver} || true
}

build() {
    cd ${srcdir}/linux-${_basever}

    make mrproper
    make headers_check
}

package() {
    cd ${srcdir}/linux-${_basever}
    make INSTALL_HDR_PATH=${pkgdir}/usr headers_install

    # use headers from libdrm
    rm -r ${pkgdir}/usr/include/drm

    # clean-up unnecessary files generated during install
    find ${pkgdir} \( -name .install -o -name ..install.cmd \) -delete
}
