# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

_srcname=libtool
pkgname=${_srcname}_p
pkgver=2.4.6
pkgrel=3
_gccver=5.2.0.r138155.g7b26e38
pkgdesc="A generic library support script"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
groups=('base-devel')
depends=('sh' 'tar' 'glibc')
makedepends=("gcc=$_gccver")
provides=("libltdl=$pkgver" "libtool-multilib=$pkgver" "libltdl_p=${pkgver}" "libtool-multilib_p=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libltdl' 'libtool-multilib')
replaces=('libltdl' 'libtool-multilib')
install=libtool.install
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${_srcname}-${pkgver}.tar.xz)
sha256sums=('7c87a8c2c8c0fc9cd5019e402bed4292462d00a718a7cd5f11218153bf28b26f')

build() {
    cd ${srcdir}/${_srcname}-${pkgver}
    ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
    make ${MAKEFLAGS}
}

check() {
    cd ${srcdir}/${_srcname}-${pkgver}
    make ${MAKEFLAGS} check
}

package() {
    cd ${srcdir}/${_srcname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
