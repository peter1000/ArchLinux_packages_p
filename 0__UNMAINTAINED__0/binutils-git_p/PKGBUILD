# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: James Harvey <jamespharvey20@gmail.com>
#
#
# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: libtool requires rebuilt with each new gcc version
#
#
# ADDED dependencies make looks also for: 'gcc-ada' 'isl-git_p'
#
#

_srcname=binutils-gdb
pkgname=binutils-git_p
pkgver=2.25.r85489.gab27339
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (git master developmental version)"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('glibc>=2.22' 'zlib' 'gcc-ada' 'isl-git_p')
makedepends=('git')
checkdepends=('dejagnu' 'bc')
provides=("binutils=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('binutils-multilib' 'binutils' 'binutils-git')
replaces=('binutils-multilib')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
source=("${_srcname}::git://sourceware.org/git/binutils-gdb.git#commit=ab27339")    #commit=ab27339  OK
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"
    echo $(cat binutils/configure | grep "PACKAGE_VERSION=" | sed "s|^PACKAGE_VERSION='||" | sed "s|'$||" | sed "s|\.51$||").r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
}

prepare() {
    cd "${srcdir}/${_srcname}"

    # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    mkdir ${srcdir}/binutils-build
}

build() {
    cd ${srcdir}/binutils-build

    ${srcdir}/binutils-gdb/configure --prefix=/usr \
        --with-lib-path=/usr/lib:/usr/local/lib \
        --with-bugurl=https://bugs.archlinux.org/ \
        --enable-threads --enable-shared --with-pic \
        --enable-ld=default --enable-gold --enable-plugins \
        --disable-werror --disable-gdb

    # check the host environment and makes sure all the necessary tools are available
    make configure-host

    make ${MAKEFLAGS} tooldir=/usr
}

check() {
	cd ${srcdir}/binutils-build

	# unset LDFLAGS as testsuite makes assumptions about which ones are active
	# ignore failures in gold testsuite...
	 make ${MAKEFLAGS} -k LDFLAGS="" check || true
}

package() {
	cd ${srcdir}/binutils-build
	make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install

	# Remove unwanted files
	rm ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

	# No shared linking to these files outside binutils
	rm ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
}
