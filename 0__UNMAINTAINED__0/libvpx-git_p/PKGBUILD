# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jesse Jaara	<gmail.com:	jesse.jaara>
# Contributor: Det		<gmail.com:	nimetonmaili>
# Contributor: h31		<yandex.com:	h31mail>
# Contributor: julroy67	<gmail.com:	julroy67>

_srcname=libvpx
pkgname=${_srcname}-git_p
pkgver=1.4.0.r1612.g9645cd4 		
pkgrel=1
pkgdesc="The VP8 Codec SDK (git version)"
arch=('x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('gcc-libs')
makedepends=('git' 'yasm')
provides=("libvpx=${pkgver}" 'libvpx.so' "${pkgname}=${pkgver}")
conflicts=('libvpx' 'libvpx-git')
source=("${_srcname}::git+https://chromium.googlesource.com/webm/${_srcname}.git#commit=9645cd4")		#commit=9645cd4   OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr --as=yasm \
        --enable-vp8 --enable-vp9 --enable-vp10 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-vp9-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/"
    make DIST_DIR="${pkgdir}/usr" install
    install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
