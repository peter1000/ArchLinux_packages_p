# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_srcname=curl
pkgname=${_srcname}_p
pkgver=7.45.0
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'krb5' 'libidn' 'libssh2' 'openssl' 'zlib')
options=('strip' 'debug')
provides=('libcurl.so' "curl=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('curl')
source=("http://curl.haxx.se/download/$_srcname-$pkgver.tar.gz"
        curlbuild.h)
sha1sums=('e44fc290844c1e3c9b7f6ef9926d4366d0600514'
          '0f235202e0d6fb4068b42b492ff5f8d6592726c1')

build() {
    cd "$_srcname-$pkgver"

    ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --with-gssapi \
        --with-libidn \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

    make ${MAKEFLAGS}
}

package() {
    cd "$_srcname-$pkgver"

    make DESTDIR="$pkgdir" install

    local ptrsize=$(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d')
    case $ptrsize in
        8) _curlbuild=curlbuild-64.h ;;
        4) _curlbuild=curlbuild-32.h ;;
        *) error "unknown pointer size for architecture: %s bytes" "$ptrsize"
          exit 1
          ;;
    esac

    # license
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"

    # devel
    mv "$pkgdir/usr/include/curl/curlbuild.h" "$pkgdir/usr/include/curl/$_curlbuild"
    install -m644 "$srcdir/curlbuild.h" "$pkgdir/usr/include/curl/curlbuild.h"
}
