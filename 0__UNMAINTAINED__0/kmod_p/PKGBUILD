# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Dave Reisner <dreisner@archlinux.org>

_srcname=kmod
pkgname=${_srcname}_p
pkgver=21
pkgrel=2
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('glibc' 'zlib' 'xz')
makedepends=('gtk-doc')
checkdepends=('linux-headers')
options=('strip' 'debug')
provides=('module-init-tools=3.16' 'libkmod.so' "kmod=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('module-init-tools' 'kmod')
replaces=('module-init-tools')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$_srcname/$_srcname-$pkgver.tar.xz"
        '0001-libkmod-module-fix-return-code-in-error-path.patch'
        "depmod-search.conf")
sha1sums=('b2c1a0a1f2979fd29c7b0825ed19254c332246b4'
          'a9bed821cf692541ab845943a9e882352cdfa0f8'
          'a1f6f6dbfaa93bcd32e0153b08fb579ef7746817')


prepare() {
    cd "$_srcname-$pkgver"

    patch -Np1 <../0001-libkmod-module-fix-return-code-in-error-path.patch
}

build() {
    cd "$_srcname-$pkgver"

    ./configure \
        --sysconfdir=/etc \
        --enable-gtk-doc \
        --with-xz \
        --with-zlib

    make ${MAKEFLAGS}
}

check() {
    # As of kmod v20, the test suite needs to build some kernel modules, and thus
    # needs headers available in order to run. We depend on linux-headers, but
    # this is really only to try and make sure that *some* useable tree of kernel
    # headers exist. The first useable tree we find is good enough, as these
    # modules will never be loaded by tests.

    local kdirs=(/usr/lib/modules/*/build/Makefile)
    if [[ ! -f ${kdirs[0]} ]]; then
        printf '==> Unable to find kernel headers to build modules for tests\n' >&2
        return 1
    fi

    local kver kdir=${kdirs[0]%/Makefile}
    IFS=/ read _ _ _ kver _ <<<"$kdir"

    make ${MAKEFLAGS} -C "$_srcname-$pkgver" check KDIR="$kdir" KVER="$kver"
}

package() {
    make -C "$_srcname-$pkgver" DESTDIR="$pkgdir" install

    # extra directories
    install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d

    for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
        ln -s kmod "$pkgdir/usr/bin/$tool"
    done

    # install depmod.d file for search/ dir
    install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}
