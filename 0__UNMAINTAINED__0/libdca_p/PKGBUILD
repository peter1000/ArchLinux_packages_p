# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

_srcname=libdca
pkgname=${_srcname}_p
pkgver=0.0.5
pkgrel=4
pkgdesc="Free library for decoding DTS Coherent Acoustics streams"
arch=('x86_64')
license=('GPL')
url="http://www.videolan.org/developers/libdca.html"
depends=('sh')
provides=('libdts' 'libdts_p' "libdca=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libdts' 'libdca')
options=('!emptydirs')
source=("http://download.videolan.org/pub/videolan/${_srcname}/${pkgver}/${_srcname}-${pkgver}.tar.bz2")
sha256sums=('dba022e022109a5bacbe122d50917769ff27b64a7bba104bd38ced8de8510642')

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    ./configure --prefix=/usr --mandir=/usr/share/man --disable-static
    make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_srcname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    rm -f "${pkgdir}/usr/lib/libdts.a"   #  points to non-existing /usr/lib/libdca.a
    rm -f "${pkgdir}/usr/share/man/man1"/{extract_dts,dtsdec}.1
    ln -s dcadec.1.gz "${pkgdir}/usr/share/man/man1/dtsdec.1.gz"
    ln -s extract_dca.1.gz "${pkgdir}/usr/share/man/man1/extract_dts.1.gz"
}
