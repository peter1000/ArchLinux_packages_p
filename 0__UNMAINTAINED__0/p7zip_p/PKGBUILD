# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: TuxSpirit<tuxspirit@archlinux.fr>  2007/11/17 21:22:36 UTC
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_srcname=p7zip
pkgname=${_srcname}_p
pkgver=9.38.1
pkgrel=3
pkgdesc="Command-line file archiver with high compression ratio"
arch=('x86_64')
url="http://p7zip.sourceforge.net/"
license=('LGPL' 'custom:unRAR')
depends=('gcc-libs' 'sh')
makedepends=('yasm')
install=$_srcname.install
provides=("p7zip=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('p7zip')
source=(https://downloads.sourceforge.net/project/$_srcname/$_srcname/$pkgver/${_srcname}_${pkgver}_src_all.tar.bz2)
sha256sums=('fd5019109c9a1bf34ad3257d37a6853eae8151ff50345f0a3ffba7d8c5fdb995')

prepare() {
    cd "$srcdir/${_srcname}_$pkgver"
    cp makefile.linux_amd64_asm makefile.machine
}

build() {
    cd "$srcdir/${_srcname}_$pkgver"
    make ${MAKEFLAGS} all3 OPTFLAGS="$CFLAGS"
}

package() {
    cd "$srcdir/${_srcname}_$pkgver"

    make install \
        DEST_DIR="$pkgdir" \
        DEST_HOME=/usr \
        DEST_MAN=/usr/share/man

    install -d "${pkgdir}"/usr/share/licenses/${pkgname}
    ln -s -t "$pkgdir/usr/share/licenses/${pkgname}" \
        /usr/share/doc/p7zip/DOC/License.txt \
        /usr/share/doc/p7zip/DOC/unRarLicense.txt

    chmod -R a+r,u+w,a+X "$pkgdir/usr"
}
