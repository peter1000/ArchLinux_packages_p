# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: rabyte <rabyte.at.gmail.dot.com>
# Contributor: Johannes Krampf <wuischke.at.amule.dot.org>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>


_srcname=midori
pkgname=${_srcname}-gtk3_p
pkgver=0.5.10
pkgrel=1
pkgdesc='Lightweight web browser (GTK3)'
arch=('x86_64' 'i686')
url='http://www.midori-browser.org/'
license=('LGPL2.1')
install="${_srcname}.install"
provides=("midori=${pkgver}" "midori-gtk3=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('midori' 'midori-gtk3')

depends=('webkitgtk3' 'libnotify' 'gcr' 'libxss' 'granite' 'gst-plugins-base' 'gst-plugins-good' 'gst-libav')
makedepends=('pkg-config' 'libxml2' 'sqlite' 'glib2' 'libsoup' 'gtk3' 'vala' 'librsvg' 'cmake' 'ninja' 'intltool')
options=('!emptydirs')
source=("http://midori-browser.org/downloads/${_srcname}_${pkgver}_all_.tar.bz2")
sha256sums=('702344f68d7f034866a46398e35b3c16a5a5f3e431a5d916ea5efc3eaaa3e46f')

build() {
    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DUSE_GTK3=ON \
        -DHALF_BRO_INCOM_WEBKIT2=OFF \
        -DUSE_ZEITGEIST=OFF \
        -DUSE_GRANITE=ON \
        -DUSE_APIDOCS=OFF \
        -DUSE_GIR=OFF \
        -DEXTRA_WARNINGS=OFF \
        -G Ninja
    ninja ${MAKEFLAGS}
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
}
