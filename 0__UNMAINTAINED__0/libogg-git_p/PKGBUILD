# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>

_srcname=libogg
pkgname=${_srcname}-git_p
_tag=1.3.2
pkgver=1.3.2.r468.g6c36ab3              
pkgrel=1
pkgdesc="Ogg bitstream and framing library"
arch=('x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('glibc')
provides=("libogg=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libogg' 'libogg-git')
source=("${_srcname}::git+https://git.xiph.org/ogg.git#commit=6c36ab3"  		#commit=6c36ab3  OK
    'config_types.h')
sha256sums=('SKIP'
            'c8ae64fa07700f97031d48fa0234c900c86fd8be9adfcf47c5599daa2e7f3a27')

pkgver() {
    cd "${srcdir}/${_srcname}"
    printf "${_tag}.r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"
    make DESTDIR="$pkgdir" install

    # install BSD license
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"

    # Resolve multilib conflict
    if [[ $CARCH == "x86_64" ]]; then
        mv "$pkgdir"/usr/include/ogg/config_types{,-64}.h
        install -m 644 "$srcdir/config_types.h" "${pkgdir}/usr/include/ogg/config_types.h"
    fi
}
