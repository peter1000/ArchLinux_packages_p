# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jan de Groot <jgc@archlinux.org>

_srcname=glib2
pkgbase=${_srcname}_p
pkgname=('glib2_p' 'glib2-docs_p')
pkgver=2.46.1
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=('x86_64')
makedepends=('pkg-config' 'python2' 'libxslt' 'docbook-xml' 'pcre' 'libffi' 'libelf')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver:0:4}/glib-$pkgver.tar.xz
        revert-warn-glib-compile-schemas.patch)
sha256sums=('5a1f03b952ebc3a7e9f612b8724f70898183e31503db329b4f15d07163c8fdfb'
            '049240975cd2f1c88fbe7deb28af14d4ec7d2640495f7ca8980d873bb710cc97')

prepare() {
    cd glib-$pkgver
    patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
}
  
build() {
    cd glib-$pkgver

    ## Prevent runtime unloading of glib
    # https://bugs.archlinux.org/task/46619
    # https://bugzilla.gnome.org/show_bug.cgi?id=755609
    LDFLAGS+=" -Wl,-z,nodelete"

    PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libdir=/usr/lib \
        --sysconfdir=/etc \
        --with-pcre=system \
        --disable-fam
    make ${MAKEFLAGS}
}

check() {
    cd glib-$pkgver
    make ${MAKEFLAGS} -k check || :
}

package_glib2_p() {
    depends=('pcre' 'libffi')
    optdepends=('python2: for gdbus-codegen and gtester-report'
              'libelf: gresource inspection tool')
    provides=("glib2=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('glib2')
    options=('!docs' '!emptydirs')
    license=('LGPL')

    cd glib-$pkgver
    make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

    for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
        chmod -x "$_i"
    done

    # Our gdb does not ship the required python modules, so remove it
    rm -rf "$pkgdir/usr/share/gdb/"
}

package_glib2-docs_p() {
    pkgdesc="Documentation for glib2"
    provides=("glib2-docs=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('gobject2-docs' 'glib2-docs')
    replaces=('gobject2-docs')
    license=('custom')
    options=('docs' '!emptydirs')

    cd glib-$pkgver/docs
    make DESTDIR="${pkgdir}" install
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 reference/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

    rm -rf "${pkgdir}/usr/share/man"
}
