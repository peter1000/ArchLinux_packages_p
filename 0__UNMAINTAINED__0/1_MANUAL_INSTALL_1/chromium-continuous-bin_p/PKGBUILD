# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Det
# Contributors: Ner0, alexwizard, thotypous, jdhore, xduugu, randypenguin, bdheeman, AlK

_srcname=chromium-continuous-bin
pkgname=${_srcname}_p
pkgver=48.0.2548.0.r356310
pkgrel=1
pkgdesc="The open-source project behind Google Chrome (Continuous, auto-tested Snapshot builds)"
arch=('x86_64')
url="https://build.chromium.org"
license=('BSD')
depends=('gtk2' 'nss' 'alsa-lib' 'xdg-utils' 'bzip2' 'libevent' 'libxss' 'icu'
         'libexif' 'libgcrypt' 'ttf-font' 'systemd' 'dbus' 'flac' 'snappy'
         'speech-dispatcher' 'pciutils' 'libpulse' 'harfbuzz' 'libsecret'
         'perl' 'perl-file-basedir' 'desktop-file-utils' 'hicolor-icon-theme'
         'gconf' 'chromium-pepper-flash-dev')
optdepends=('kdebase-kdialog: needed for file dialogs in KDE'
            'gnome-keyring: for storing passwords in GNOME keyring'
            'kdeutils-kwalletmanager: for storing passwords in KWallet')
provides=("chromium=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('chromium' 'chromium-continuous-bin')
install=$_srcname.install
options=(!strip)
source=('chromium-continuous-bin'
        'chromium-continuous-bin.desktop'
        'LICENSE')
sha256sums=('cd608ffc3b774bfebb02f48f5f59fd62ccba1e2c74a9c0311be94fe0290e75a0'
            '9221399e33b7b0e9405160ac66190bfa6025c7d2d9bed0d73e0d0758a2062c13'
            '7a209dd1b94cabdb5ea9c6f9164b9546ffa5daaa671e7767d49510db055f5c51')
sha256sums_x86_64=('b722ff637a88f8ca08c3e1731cbf0d7d21a815b49436c2f13951ee97c1d546bd')

[[ $CARCH = x86_64 ]] && _64="_x64"
_build=$(curl -s "http://commondatastorage.googleapis.com/chromium-browser-continuous/Linux$_64/LAST_CHANGE")
source_x86_64=("chrome-linux-r$_build.zip::http://commondatastorage.googleapis.com/chromium-browser-continuous/Linux_x64/$_build/chrome-linux.zip")

pkgver() {
  echo $(chrome-linux/chrome --version | cut -d " " -f2).r$_build
}

package() {
	msg2 "Creating directory structure..."
	install -d "$pkgdir"/opt/
	install -d "$pkgdir"/usr/bin/
	install -d "$pkgdir"/usr/share/applications/
	install -d "$pkgdir"/usr/share/licenses/${pkgname}/
	install -d "$pkgdir"/usr/share/man/man1/
	install -d "$pkgdir"/usr/share/pixmaps/

	msg2 "Making it nice..."
	# Permissions
	find -type d -exec chmod 755 {} ';'
	find -type f -exec chmod +r {} ';'
	cd chrome-linux
	chmod 755 chrome chrome-wrapper nacl_* xdg-*
	chmod 4755 chrome_sandbox
	cd ..

	msg2 "Moving contents..."
	# Main script
	install -m755 chromium-continuous-bin "$pkgdir"/usr/bin/

	# Rename chrome-sandbox
	mv chrome-linux/chrome{_,-}sandbox

	# Desktop
	install -m644 chromium-continuous-bin.desktop "$pkgdir"/usr/share/applications/

	# Icon
	mv chrome-linux/product_logo_48.png "$pkgdir"/usr/share/pixmaps/chromium-continuous-bin.png

	# Man page
	gzip -9 chrome-linux/chrome.1
	mv chrome-linux/chrome.1.gz "$pkgdir"/usr/share/man/man1/chromium-continuous-bin.1.gz

	# License
	install -m644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/

	msg2 "Installing main directory..."
	mv chrome-linux/ "$pkgdir"/opt/chromium-continuous
}
