# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=yasm
pkgname=${_srcname}_p
pkgver=1.3.0
pkgrel=1
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.)"
arch=('x86_64')
url="http://www.tortall.net/projects/yasm/"
license=('BSD' 'GPL2' 'LGPL2.1' 'PerlArtistic')
depends=('glibc')
options=('staticlibs')
provides=("yasm=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('yasm')
source=(http://www.tortall.net/projects/yasm/releases/${_srcname}-${pkgver}.tar.gz)
sha1sums=('b7574e9f0826bedef975d64d3825f75fbaeef55e')

build() {
    cd ${_srcname}-${pkgver}
    ./configure --prefix=/usr
    make ${MAKEFLAGS}
}

check() {
    cd ${_srcname}-${pkgver}
    make ${MAKEFLAGS} check
}

package() {
    cd ${_srcname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 BSD.txt "${pkgdir}/usr/share/licenses/${pkgname}/BSD.txt"
}
