# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=transfig
pkgname=${_srcname}_p
pkgver=3.2.5e
pkgrel=1
pkgdesc="Format conversion utility that can be used with xfig"
arch=('x86_64')
url="http://www.xfig.org"
license=('custom')
depends=('libpng' 'libxpm' 'bc')
makedepends=('imake')
provides=("transfig=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('transfig')
source=(http://downloads.sourceforge.net/mcj/${_srcname}.${pkgver}.tar.gz LICENSE)
sha1sums=('27aa9691bf84f8775db9be39c453a8132148bad1'
          'b8b712871615308b8b6add92f86d218437d652f2')

prepare() {
    cd ${_srcname}.${pkgver}
    sed -i 's/XCOMM USELATEX2E/USELATEX2E/' transfig/Imakefile
    sed -i 's/XCOMM USEINLINE/USEINLINE/' fig2dev/Imakefile
}

build() {
    cd ${_srcname}.${pkgver}
    xmkmf
    make ${MAKEFLAGS} FIG2DEV_LIBDIR=/usr/share/fig2dev Makefiles
    make ${MAKEFLAGS} FIG2DEV_LIBDIR=/usr/share/fig2dev XFIGLIBDIR=/usr/share/xfig
}

package() {
    cd ${_srcname}.${pkgver}
    make DESTDIR="${pkgdir}" XFIGLIBDIR=/usr/share/xfig \
        FIG2DEV_LIBDIR=/usr/share/fig2dev MANPATH=/usr/share/man \
        install install.man
    mv "${pkgdir}/usr/bin/fig2ps2tex.sh" "${pkgdir}/usr/bin/fig2ps2tex"
    install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
