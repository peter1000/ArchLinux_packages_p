# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

_srcname=acl
pkgname=${_srcname}_p
pkgver=2.2.52
pkgrel=2
pkgdesc="Access control list utilities, libraries and headers"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
depends=('attr>=2.4.46')
provides=('xfsacl' "acl=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('acl' 'xfsacl')
replaces=('xfsacl')
source=("http://download.savannah.gnu.org/releases/$_srcname/$_srcname-$pkgver.src.tar.gz")
sha256sums=('179074bb0580c06c4b4137be4c5a92a701583277967acdb5546043c7874e0d23')

build() {
    cd "$_srcname-$pkgver"

    export INSTALL_USER=root INSTALL_GROUP=root
    ./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib
    make ${MAKEFLAGS}
}

package() {
    make -C "$_srcname-$pkgver" DIST_ROOT="$pkgdir" install install-lib install-dev
}
