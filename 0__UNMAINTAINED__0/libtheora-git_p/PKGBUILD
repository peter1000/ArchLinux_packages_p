# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Tom Killian <tom@archlinux.org>
# Committer: dorphell <dorphell@archlinux.org>

_srcname=libtheora
pkgname=${_srcname}-git_p
_tag=1.2.0alpha1
pkgver=1.2.0alpha1.r124.gfbb2758                
pkgrel=1
pkgdesc="An open video codec developed by the Xiph.org"
arch=('x86_64')
url="http://www.xiph.org"
license=('BSD')
depends=('libogg' 'libvorbis')
makedepends=('git')
provides=("libtheora=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libtheora')
source=("${_srcname}::git+https://git.xiph.org/theora.git#commit=fbb2758")			#commit=fbb2758  OK
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"
    printf "${_tag}.r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr --enable-shared --disable-static
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"
    make DESTDIR="${pkgdir}" install

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 LICENSE COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
