# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

_srcname=dialog
pkgname=${_srcname}_p
pkgver=1.2_20150920
pkgrel=1
epoch=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('x86_64')
url="http://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('sh' 'ncurses')
provides=("dialog=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('dialog')
source=(ftp://invisible-island.net/$_srcname/$_srcname-${pkgver/_/-}.tgz)
sha256sums=('c4e61ec5768701683dd4b5b2ebd8a31e6289fa6a1f5801e4b481085650698c05')

build() {

    cd "$srcdir/$_srcname-${pkgver/_/-}"

    ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --with-ncursesw \
        --with-shared \
        --with-versioned-syms \
        --enable-nls
    make ${MAKEFLAGS}
}

package() {
    cd "$srcdir/$_srcname-${pkgver/_/-}"
    make DESTDIR="$pkgdir" install-full
}
