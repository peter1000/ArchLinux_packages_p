# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

_srcname=texinfo
pkgname=${_srcname}_p
pkgver=5.2
pkgrel=4
pkgdesc='GNU documentation system for on-line information and printed output'
arch=('x86_64')
url='http://www.gnu.org/software/texinfo/'
license=('GPL3')
groups=('base' 'base-devel')
depends=('ncurses' 'findutils' 'gzip' 'perl' 'sh')
install=texinfo.install
provides=("texinfo=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('texinfo')
source=(ftp://ftp.gnu.org/pub/gnu/$_srcname/${_srcname}-${pkgver}.tar.xz
	texinfo-5.2-C-n-fix.patch)
sha256sums=('5471ef683a64588a7cfef46ef2bdd3fbcbca89d847e10832612293f105e44eda'
            '1a0a3b79d00b79157a8b9ac1d8307cd4ea252594a6e9e8d7b73931e7331618b9')

prepare() {
	cd "${_srcname}-${pkgver}"
	patch -p0 -i ../texinfo-5.2-C-n-fix.patch
}

build() {
	cd "${_srcname}-${pkgver}"
	./configure --prefix=/usr
	make ${MAKEFLAGS}
}

check() {
	make ${MAKEFLAGS} -C "${_srcname}-${pkgver}" check
}

package() {
	make -C "${_srcname}-${pkgver}" DESTDIR="$pkgdir" install
}
