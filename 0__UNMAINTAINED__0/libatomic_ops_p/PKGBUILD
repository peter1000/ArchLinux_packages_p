# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Adam Walters <abwalters@abwalters.us>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=libatomic_ops
pkgver=7.4.2
_tag=$pkgname-${pkgver//./_}
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('x86_64')
url="https://github.com/ivmai/libatomic_ops"
depends=('glibc')
license=('GPL2' 'MIT')
provides=("libatomic_ops=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libatomic_ops')
source=(https://github.com/ivmai/libatomic_ops/archive/$_tag.zip)
sha1sums=('8f0831cac2e25ec4e87e3d71b047f7af9f46ed32')

build() {
    cd $pkgname-$_tag
    ./autogen.sh
    ./configure --prefix=/usr --disable-static --enable-shared
    make ${MAKEFLAGS}
}

check() {
    cd $pkgname-$_tag
    make ${MAKEFLAGS} check
}

package() {
    cd $pkgname-$_tag
    make DESTDIR="$pkgdir" install
    install -D -m644 doc/LICENSING.txt "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
    rm -r "$pkgdir/usr/share/libatomic_ops"
}
