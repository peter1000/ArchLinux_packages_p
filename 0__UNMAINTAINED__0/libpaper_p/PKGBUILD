# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: royrocks <royrocks13@gmail.com>

_srcname=libpaper
pkgname=${_srcname}_p
pkgver=1.1.24
pkgrel=8
pkgdesc='Library for handling paper characteristics'
arch=('x86_64')
url='http://packages.debian.org/unstable/source/libpaper'
license=('GPL')
depends=('sh')
backup=('etc/papersize')
provides=("libpaper=${pkgver}" "${_srcname}=${pkgver}")
conflicts=('libpaper')
source=("http://ftp.de.debian.org/debian/pool/main/libp/$_srcname/${_srcname}_$pkgver+nmu4.tar.gz")
sha1sums=('830c7e5c0f77d4f8a314ba3ea93dfd954bdd3a4e')

build() {
    cd "$_srcname-$pkgver+nmu4"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin
    make ${MAKEFLAGS}
}

package() {
    cd "$_srcname-$pkgver+nmu4"
    make DESTDIR="$pkgdir" install

    # add systemwide default papersize read by many office applications
    install -dm 755 $pkgdir/etc
    echo '# Simply write the paper name. See papersize(5) for possible values' > $pkgdir/etc/papersize

    # add libpaper.d directory other packages can use to store files
    install -dm 755 $pkgdir/etc/libpaper.d

    # add localisation
    pushd debian/po
    for i in `ls *.po`; do
        install -dm 755 ${pkgdir}/usr/share/locale/${i%.po}/LC_MESSAGES/;
        msgfmt $i -o ${pkgdir}/usr/share/locale/${i%.po}/LC_MESSAGES/${_srcname}.mo;
    done
    popd
}
