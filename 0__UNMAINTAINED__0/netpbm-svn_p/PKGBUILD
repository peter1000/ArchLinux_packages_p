# Maintainer: peter1000 <https://github.com/peter1000>

_srcname=netpbm
pkgname=${_srcname}-svn_p
pkgver=10.72.02.r2654
pkgrel=1
pkgdesc="A toolkit for manipulation of graphic images"
arch=('x86_64')
license=('custom' 'BSD' 'GPL' 'LGPL')
url="http://netpbm.sourceforge.net/"
depends=('perl' 'libpng' 'libtiff' 'libxml2')
makedepends=('python2' 'jbigkit' 'subversion')
options=('!makeflags')
# Get docs with: wget --recursive --relative -nH http://netpbm.sourceforge.net/doc/
provides=("netpbm=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('netpbm')
source=("${_srcname}::svn://svn.code.sf.net/p/netpbm/code/advanced#revision=2654"    #revision=2654  OK
        https://sources.archlinux.org/other/packages/netpbm/netpbm-doc-31Jan2014.tar.xz
        netpbm-CAN-2005-2471.patch 
        netpbm-security-scripts.patch)
sha1sums=('SKIP'
          '25a58975bd8cd383238b4ae31f4405c60914faac'
          'b79cf9d42488fea065ba16262ed97694c47af08d'
          'c67a63c010353de3ecac3ad72ba76bbb85e4ed4c')

pkgver() {
    cd "${srcdir}/${_srcname}"
    local ver="$(svnversion)"
    local _tag=$(grep MAJOR version.mk |cut -d ' ' -f 3).$(grep MINOR version.mk |cut -d ' ' -f 3).0$(grep POINT version.mk |cut -d ' ' -f 3)
    printf "%s.r%s" "${_tag}" "${ver//[[:alpha:]]}"
}


prepare() {
    cd "${srcdir}/${_srcname}"

    patch -p1 < ../netpbm-CAN-2005-2471.patch
    patch -p1 < ../netpbm-security-scripts.patch
    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' buildtools/makeman
    sed -i 's|@python|@python2|' buildtools/manpage.mk

    cp config.mk.in  config.mk
    [ "${CARCH}" = 'x86_64' ] && echo 'CFLAGS_SHLIB = -fPIC' >> config.mk
    echo "NETPBM_DOCURL = file://${srcdir}/doc" >> config.mk
    echo 'TIFFLIB = libtiff.so' >> config.mk
    echo 'JPEGLIB = libjpeg.so' >> config.mk
    echo 'PNGLIB = libpng.so' >> config.mk
    echo 'ZLIB = libz.so' >> config.mk
    echo 'JBIGLIB = /usr/lib/libjbig.a' >> config.mk

    sed -i 's|misc|share/netpbm|' common.mk
    sed -i 's|/link|/lib|' lib/Makefile
    sed -i 's|install.manwebmain install.manweb install.man|install.man|' GNUmakefile
}

build() {
    cd "${srcdir}/${_srcname}"
    make ${MAKEFLAGS}
    
    # Generating useful man pages with html doc
    make MAKEMAN="${srcdir}/${_srcname}/buildtools/makeman" USERGUIDE=. \
        -C ../doc -f "${srcdir}/${_srcname}/buildtools/manpage.mk" manpages
}

package() {
    cd "${srcdir}/${_srcname}"
    make pkgdir="${pkgdir}/usr" PKGMANDIR=share/man install-run install-dev

    # Removing dummy man pages
    rm "${pkgdir}"/usr/share/man/man{1,3,5}/*

    make MAKEMAN="${srcdir}/${_srcname}/buildtools/makeman" MANDIR="${pkgdir}/usr/share/man" \
        -C ../doc -f "${srcdir}/${_srcname}/buildtools/manpage.mk" installman


    # Replace obsolete utility
    echo -e '#!/bin/sh\npamditherbw $@ | pamtopnm\n' > "${pkgdir}/usr/bin/pgmtopbm"

    # Licensing.  Note that each program in the package has a separate license.
    install -D -m644 "${srcdir}/${_srcname}/doc/copyright_summary" \
        "${pkgdir}/usr/share/licenses/${pkgname}/copyright_summary.txt"
}
