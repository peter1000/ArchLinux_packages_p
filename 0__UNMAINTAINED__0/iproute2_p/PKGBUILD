# $Id: PKGBUILD 248062 2015-10-01 16:33:08Z foutrelis $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

_srcname=iproute2
pkgname=${_srcname}_p
pkgver=4.1.1
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="http://www.linuxfoundation.org/collaborate/workgroups/networking/iproute2"
depends=('glibc' 'iptables')
makedepends=('linux-atm')
optdepends=('linux-atm: ATM support')
groups=('base')
options=('staticlibs' '!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
provides=("iproute=${pkgver}" "iproute2=${pkgver}" "${_srcname}=${pkgver}")
conflicts=('iproute')
replaces=('iproute')
source=("http://www.kernel.org/pub/linux/utils/net/${_srcname}/${_srcname}-${pkgver}.tar.xz"
        iproute2-fhs.patch
        unwanted-link-help.patch)
sha1sums=('750c17e720b43041c23fed7b8cf0118c0e841036'
          '2dc6d8f1a2495a0d51eaa303dcc78ecc0c477935'
          '3b1335f4025f657f388fbf4e5a740871e3129c2a')

prepare() {
    cd "${srcdir}/${_srcname}-${pkgver}"

    # set correct fhs structure
    patch -Np1 -i "${srcdir}/iproute2-fhs.patch"

    # allow operations on links called "h", "he", "hel", "help"
    patch -Np1 -i "${srcdir}/unwanted-link-help.patch"

    # do not treat warnings as errors
    sed -i 's/-Werror//' Makefile
}

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"

    ./configure
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    # libnetlink isn't installed, install it FS#19385
    install -Dm644 include/libnetlink.h "${pkgdir}/usr/include/libnetlink.h"
    install -Dm644 lib/libnetlink.a "${pkgdir}/usr/lib/libnetlink.a"

    # move binaries
    cd "${pkgdir}"
    mv sbin usr/bin
}
