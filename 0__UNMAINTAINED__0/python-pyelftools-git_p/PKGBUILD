# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

_srcname=pyelftools
pkgname=python-${_srcname}-git_p
pkgver=0.23.r24.gd7aec6a                #commit=d7aec6a  OK
pkgrel=1
pkgdesc="Python library for analyzing ELF files and DWARF debugging information"
arch=('any')
url="https://github.com/eliben/pyelftools"
license=('custom')
depends=('python')
makedepends=('git')
provides=("python-pyelftools=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('python-pyelftools' 'python-pyelftools-git')
source=("${_srcname}::git+https://github.com/eliben/${_srcname}.git#commit=d7aec6a")
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    python setup.py build
}

check() {
  cd "${srcdir}/${_srcname}"
  python test/run_all_unittests.py
}

package() {
    cd "${srcdir}/${_srcname}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
