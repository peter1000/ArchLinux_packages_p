# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Peter Mattern <pmattern at arcor dot de>

_srcname=qps
pkgname=${_srcname}-git_p
pkgver=r23.gebc5bd4               
pkgrel=1
pkgdesc="Qt process manager"
arch=("x86_64")
url="https://github.com/QtDesktop/qps"
license=("unknown")
depends=("qt5-x11extras" "libxkbcommon-x11" "hicolor-icon-theme")
makedepends=("git" "cmake" "qt5-tools" 'ninja')
provides=("qps=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('qps' 'qps-git')
source=("${_srcname}::git+https://github.com/QtDesktop/${_srcname}.git#commit=ebc5bd4")     #commit=ebc5bd4  OK
sha256sums=("SKIP")

pkgver() {
    cd "${srcdir}/${_srcname}"
    printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    mkdir build
    cd build
    cmake -G Ninja "${srcdir}/${_srcname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    ninja ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/build"
    DESTDIR="$pkgdir" ninja install
}
