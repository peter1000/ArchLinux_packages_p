# $Id: PKGBUILD 244965 2015-08-28 18:33:05Z seblu $
# Maintainer: SÃ©bastien "Seblu" Luttringer
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

_srcname=pcre
pkgname=${_srcname}_p
pkgver=8.37
pkgrel=4
pkgdesc='A library that implements Perl 5-style regular expressions'
arch=('x86_64')
url='http://www.pcre.org/'
license=('BSD')
depends=('gcc-libs' 'readline' 'zlib' 'bzip2' 'bash')
provides=("pcre=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('pcre')
source=("ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/$_srcname-$pkgver.tar.bz2"
        '01-svn-snapshot.patch')
sha1sums=('4c629b3f582366fae4e8912f0d9fa3140347d6e7'
          '123ba3323e7f888503364288b7107e9500644735')

prepare() {
    # bump to latest snapshot. Too many security issues
    patch -p0 -d $_srcname-$pkgver < 01-svn-snapshot.patch
}

build() {
    cd $_srcname-$pkgver
    ./configure \
        --prefix=/usr \
        --enable-unicode-properties \
        --enable-pcre16 \
        --enable-pcre32 \
        --enable-jit \
        --enable-pcregrep-libz \
        --enable-pcregrep-libbz2 \
        --enable-pcretest-libreadline
    make ${MAKEFLAGS}
}

check() {
    cd $_srcname-$pkgver
    make -j1 check
}

package() {
    cd $_srcname-$pkgver
    make DESTDIR="$pkgdir" install

    install -Dm644 LICENCE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
