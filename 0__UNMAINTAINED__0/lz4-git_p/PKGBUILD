# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: SÃ©bastien Luttringer

_srcname=lz4
pkgname=${_srcname}-git_p
pkgver=131.r0.gd86dc91
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('x86_64')
url='https://github.com/Cyan4973/lz4'
license=('GPL2')
makedepends=('git')
checkdepends=('diffutils')
depends=('glibc')
provides=("lz4=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('lz4' 'lz4-git')
source=("${_srcname}::git+https://github.com/Cyan4973/${_srcname}.git#commit=d86dc91")     #commit=d86dc91  OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix='r'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    make ${MAKEFLAGS} PREFIX=/usr
}

check() {
    lz4/programs/lz4 /etc/passwd passwd.lz4
    lz4/programs/lz4 -d passwd.lz4 passwd
    diff -q /etc/passwd passwd
    rm passwd
}

package() {
    cd "${srcdir}/${_srcname}"
    make install PREFIX=/usr DESTDIR="$pkgdir"
}
