# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Francois Boulogne <fboulogne at april dot org>

_srcname=argh
pkgname=python-${_srcname}_p
pkgver=0.26.1
pkgrel=1
pkgdesc="An unobtrusive argparse wrapper with natural syntax"
arch=(any)
url="https://pypi.python.org/pypi/argh"
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
provides=("python-argh=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('python-argh' 'python-argh-git')
source=(https://pypi.python.org/packages/source/a/argh/argh-$pkgver.tar.gz)
sha256sums=('06a7442cb9130fb8806fe336000fcf20edf1f2f8ad205e7b62cec118505510db')

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    
    # Install Documentation
    install -D -m644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}
