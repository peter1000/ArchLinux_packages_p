# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Kyle <kyle@gmx.ca>

_srcname=opus-tools
pkgname=${_srcname}-git_p
pkgver=0.1.9.r9.gaa7e018               #commit=xxxx  OK
pkgrel=1
pkgdesc="A collection of tools for working with opus files (git version)"
arch=('x86_64')
url='http://wiki.xiph.org/Opus-tools'
license=('BSD')
depends=('opus' 'flac')
makedepends=('git')
provides=("opus-tools" "${pkgname}=${pkgver}")
conflicts=('opus-tools' 'opus-tools-git')
source=("${_srcname}::git+https://git.xiph.org/${_srcname}.git")
sha512sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    ./autogen.sh
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"
    make DESTDIR="$pkgdir/" install
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
