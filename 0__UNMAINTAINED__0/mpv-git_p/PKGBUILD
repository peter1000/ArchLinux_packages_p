# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

_srcname=mpv
pkgname=${_srcname}-git_p
pkgver=0.1.0.r6413.gb984ec5             
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('x86_64')
license=('GPL')
url='http://mpv.io'
depends=('ffmpeg' 'lcms2' 'libxss' 'libxinerama' 'libxv' 'libxkbcommon' 'desktop-file-utils' 'hicolor-icon-theme' 
    'xdg-utils' 'lua52' 'libdvdnav' 'libguess' 'libxrandr' 'smbclient' 'rubberband')
#makedepends=('git' 'mesa' 'python-docutils' 'ladspa' 'hardening-wrapper')
makedepends=('git' 'mesa' 'python-docutils' 'ladspa')
optdepends=('youtube-dl: for video-sharing websites playback')
options=('!emptydirs' '!buildflags')
install=${_srcname}.install
conflicts=('mpv' 'mpv-git')
provides=("mpv=${pkgver}" "${pkgname}=${pkgver}")
source=("${_srcname}::git+https://github.com/mpv-player/${_srcname}.git")        #commit=b984ec5   OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    ./bootstrap.py
}

build() {
    cd "${srcdir}/${_srcname}"

    ./waf configure --prefix=/usr \
        --confdir=/etc/mpv \
        --enable-zsh-comp \
        --enable-libmpv-shared \
        --enable-cdda

    ./waf build
}

package() {
    cd "${srcdir}/${_srcname}"
    ./waf install --destdir="$pkgdir"

    install -d "$pkgdir"/usr/share/doc/mpv/examples
    install -m644 etc/{input,example}.conf "$pkgdir"/usr/share/doc/mpv/examples
    install -m644 DOCS/{encoding.rst,tech-overview.txt} "$pkgdir"/usr/share/doc/mpv
}
