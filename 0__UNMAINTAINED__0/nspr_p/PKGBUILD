# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_srcname=nspr
pkgname=${_srcname}_p
pkgver=4.10.9
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=('!emptydirs')
provides=("nspr=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('nspr')
source=(https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${_srcname}-${pkgver}.tar.gz)
sha256sums=('4112ff6ad91d32696ca0c6c3d4abef6367b5dc0127fa172fcb3c3ab81bb2d881')

build() {
    cd $_srcname-$pkgver

    _confflags="--enable-64bit"

    ./nspr/configure \
        --prefix=/usr \
        --libdir=/usr/lib \
        --includedir=/usr/include/nspr \
        --enable-optimize \
        --disable-debug ${_confflags}
    make ${MAKEFLAGS}
}

package() {
    cd $_srcname-$pkgver
    make DESTDIR="$pkgdir" install

    ln -s nspr.pc "$pkgdir/usr/lib/pkgconfig/mozilla-nspr.pc"
    rm -r "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
         "$pkgdir/usr/include/nspr/md"
}
