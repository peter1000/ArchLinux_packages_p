# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_srcname=pam
pkgname=${_srcname}_p
pkgver=1.2.1
pkgrel=1
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
license=('GPL2')
url="http://linux-pam.org"
depends=('glibc' 'cracklib' 'libtirpc' 'pambase')
makedepends=('flex' 'w3m' 'docbook-xml>=4.4' 'docbook-xsl')
backup=(etc/security/{access.conf,group.conf,limits.conf,namespace.conf,namespace.init,pam_env.conf,time.conf} etc/default/passwd etc/environment)
provides=("pam=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('pam')
source=(http://linux-pam.org/library/Linux-PAM-$pkgver.tar.bz2
        https://sources.archlinux.org/other/pam_unix2/pam_unix2-2.9.1.tar.bz2
        pam_unix2-glibc216.patch)
sha1sums=('3620ab5f5e02272825c426622761a19a1a2facca'
          '08484332ff824b03b84aa9aa124c4d5f6c808487'
          '6137fb7c2d7051a5b696b6d3b44f8b1fff969abc')

options=('!emptydirs')

prepare () {
    cd $srcdir/Linux-PAM-$pkgver

    # fix pam_unix2 building
    cd $srcdir/pam_unix2-2.9.1
    patch -Np1 -i "${srcdir}/pam_unix2-glibc216.patch"
}

build() {
    cd $srcdir/Linux-PAM-$pkgver
    ./configure --libdir=/usr/lib --sbindir=/usr/bin --disable-db
    make ${MAKEFLAGS}

    cd $srcdir/pam_unix2-2.9.1
    # modify flags to build against the pam compiled here, not a system lib.
    ./configure \
        CFLAGS="$CFLAGS -I$srcdir/Linux-PAM-$pkgver/libpam/include/" \
        LDFLAGS="$LDFLAGS -L$srcdir/Linux-PAM-$pkgver/libpam/.libs/" \
        --libdir=/usr/lib \
        --sbindir=/usr/bin
    make ${MAKEFLAGS}
}

package() {
    cd $srcdir/Linux-PAM-$pkgver
    make DESTDIR=$pkgdir SCONFIGDIR=/etc/security install

    # build pam_unix2 module
    # source ftp://ftp.suse.com/pub/people/kukuk/pam/pam_unix2
    cd $srcdir/pam_unix2-2.9.1
    make DESTDIR=$pkgdir install

    # add the realtime permissions for audio users
    sed -i 's|# End of file||' $pkgdir/etc/security/limits.conf
    cat >>$pkgdir/etc/security/limits.conf <<_EOT
*               -       rtprio          0
*               -       nice            0
@audio          -       rtprio          65
@audio          -       nice           -10
@audio          -       memlock         40000
_EOT

    # fix some missing symlinks from old pam for compatibility
    cd $pkgdir/usr/lib/security
    ln -s pam_unix.so pam_unix_acct.so
    ln -s pam_unix.so pam_unix_auth.so
    ln -s pam_unix.so pam_unix_passwd.so
    ln -s pam_unix.so pam_unix_session.so

    # set unix_chkpwd uid
    chmod +s $pkgdir/usr/bin/unix_chkpwd
    # remove doc which is not used anymore
    # FS #40749
    rm $pkgdir/usr/share/doc/Linux-PAM/sag-pam_userdb.html
}
