# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

_srcname=libvorbis
pkgname=${_srcname}-git_p
_tag=1.3.5
pkgver=1.3.5.r1459.gc75b3b1             
pkgrel=1
pkgdesc='The Ogg Vorbis codec library.'
arch=('x86_64')
url='http://www.xiph.org/vorbis/'
license=('BSD')
depends=('libogg')
provides=('libvorbisfile.so' 'libvorbis.so' 'libvorbisenc.so' "libvorbis=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libvorbisfile.so' 'libvorbis.so' 'libvorbisenc.so' 'libvorbis' 'libvorbis-git')
source=("${_srcname}::git+https://git.xiph.org/vorbis.git#commit=c75b3b1")			#commit=c75b3b1  OK
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_srcname}"
    printf "${_tag}.r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr --disable-static
    make ${MAKEFLAGS}
}

check() {
      cd "${srcdir}/${_srcname}"
      make -j1 check
}

package() {
      cd "${srcdir}/${_srcname}"
      make DESTDIR="$pkgdir" install
      install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
