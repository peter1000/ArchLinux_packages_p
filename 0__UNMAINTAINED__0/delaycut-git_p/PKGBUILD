# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: sonic414 <kevinjf at live dot in>

_srcname=delaycut
pkgname=${_srcname}-git_p
pkgver=1.4.3.7+r17.cd12340               #commit=xxxx  OK
pkgrel=1
pkgdesc="Cuts and corrects delay in ac3 and dts files."
arch=('x86_64')
url='https://github.com/darealshinji/delaycut'
license=('GPL3')
depends=('qt4')
makedepends=('git')
conflicts=('delaycut' 'delaycut-git')
provides=("delaycut=${pkgver}" "${pkgname}=${pkgver}")
source=("${_srcname}::git+https://github.com/darealshinji/${_srcname}.git")
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    qmake-qt4 CONFIG+=Linux delaycut.pro
    make
}

package () {
    cd "${srcdir}/${_srcname}"
    install -Dm755 delaycut "${pkgdir}"/usr/bin/delaycut
    install -Dm644 extra/delaycut.desktop "${pkgdir}"/usr/share/applications/delaycut.desktop
    install -Dm644 extra/delaycut.xpm "${pkgdir}"/usr/share/pixmaps/delaycut.xpm
}
