# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jan de Groot <jgc@archlinux.org>

# This package spans multiple repositories. 
# Always build from cyrus-sasl/trunk and merge changes to libsasl/trunk.

_srcname=cyrus-sasl
pkgbase=${_srcname}_p
pkgname=('libsasl_p' 'cyrus-sasl_p' 'cyrus-sasl-gssapi_p' 'cyrus-sasl-ldap_p' 'cyrus-sasl-sql_p')
pkgver=2.1.26
pkgrel=7
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('x86_64')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
options=('!makeflags')
makedepends=('postgresql-libs' 'libmariadbclient' 'libldap' 'krb5' 'openssl' 'sqlite')
source=(ftp://ftp.cyrusimap.org/cyrus-sasl/${_srcname}-${pkgver}.tar.gz
        cyrus-sasl-2.1.22-qa.patch
        cyrus-sasl-2.1.26-size_t.patch
        0010_maintainer_mode.patch
        0011_saslauthd_ac_prog_libtool.patch
        0025_ld_as_needed.patch
        0026_drop_krb5support_dependency.patch
        0030-dont_use_la_files_for_opening_plugins.patch
        saslauthd.service
        saslauthd.conf.d
        tmpfiles.conf
        CVE-2013-4122.patch
        cyrus-sasl-sql.patch)
sha256sums=('8fbc5136512b59bb793657f36fadda6359cae3b08f01fd16b3d406f1345b7bc3'
            '56dbdc290871f3a42e507fe0be90431de15a832da7cf99bf3c21fb5aef05c8ac'
            'ca80fa4ea0e5484c8e830528201f01b3ac988f8ba1b80b1a954c9f2904b224e2'
            '086f3b4234fc5df397b2403f06e76be9cd59bb3a2500878abc9054611e8f1267'
            'bd00ac9dc19af23eceded66b3dd8a649d98dfb1546cca0a96cee4d6bb478ace4'
            '5143036f20fdc1ff0b44b73b6d245392edc2f786d74730fc0f8f75d7b40ea5c6'
            '3924aa23aa60dce59b2714a45a6479349fefdf973183ff1bc0348f11353a3baa'
            '1e7dc061fda7736cb61c717dff656642e3b9ead6fb5ec046770ef792cfbea70c'
            '5c6453050a5f594ca1d53baf3a6188d8f3cdc7a28467ad7c844ad51f663dae9a'
            'fa57b4f374ae633633091b1c8b44e1e0be814e4fddbfa75f16eb3dd1f16b8640'
            '16ca1a4185847b0c6c70ef6c7c314cb466f698d3ed02185a5f50e8179822f024'
            'aeb707ad63333cc48a8ba05807021b818d85006b45875c4f488bf8f782aef50b'
            '09b6536dd09a665051bb61c5957c9b0a2a64e32b42f9f5d5c5858ac76da01e5c')

prepare() {
  cd ${_srcname}-${pkgver}
  patch -Np1 -i ../cyrus-sasl-2.1.22-qa.patch
  patch -Np1 -i ../cyrus-sasl-2.1.26-size_t.patch
  patch -Np1 -i ../0010_maintainer_mode.patch
  patch -Np1 -i ../0011_saslauthd_ac_prog_libtool.patch
  patch -Np1 -i ../0025_ld_as_needed.patch
  patch -Np1 -i ../0026_drop_krb5support_dependency.patch
  patch -Np1 -i ../0030-dont_use_la_files_for_opening_plugins.patch
  patch -Np1 -i ../CVE-2013-4122.patch
  patch -Np0 -i ../cyrus-sasl-sql.patch

  sed 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.in
}

build() {
    cd ${_srcname}-${pkgver}

    rm -f config/config.guess config/config.sub 
    rm -f config/ltconfig config/ltmain.sh config/libtool.m4
    rm -fr autom4te.cache
    libtoolize -c
    aclocal -I config -I cmulocal
    automake -a -c
    autoheader
    autoconf

    pushd saslauthd
    rm -f config/config.guess config/config.sub 
    rm -f config/ltconfig config/ltmain.sh config/libtool.m4
    rm -fr autom4te.cache
    libtoolize -c
    aclocal -I config -I ../cmulocal -I ../config
    automake -a -c
    autoheader
    autoconf
    popd

    ./configure --prefix=/usr \
        --sbin=/usr/bin \
        --mandir=/usr/share/man \
        --infodir=/usr/share/info \
        --disable-static \
        --enable-shared \
        --enable-alwaystrue \
        --enable-checkapop \
        --enable-cram \
        --enable-digest \
        --disable-otp \
        --disable-srp \
        --disable-srp-setpass \
        --disable-krb4 \
        --enable-gssapi \
        --enable-auth-sasldb \
        --enable-plain \
        --enable-anon \
        --enable-login \
        --enable-ntlm \
        --disable-passdss \
        --enable-sql \
        --with-mysql \
        --with-pgsql=/usr/lib \
        --with-sqlite3=/usr/lib \
        --enable-ldapdb \
        --disable-macos-framework \
        --with-pam \
        --with-saslauthd=/var/run/saslauthd \
        --with-ldap \
        --with-dblib=gdbm \
        --with-configdir=/etc/sasl2:/etc/sasl:/usr/lib/sasl2 \
        --sysconfdir=/etc \
        --with-devrandom=/dev/urandom
    make ${MAKEFLAGS}
}

package_libsasl_p() {
    pkgdesc="Cyrus Simple Authentication Service Layer (SASL) Library"
    depends=('openssl')
    provides=("libsasl=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('libsasl' 'cyrus-sasl-plugins')
    replaces=('cyrus-sasl-plugins')

    cd ${_srcname}-${pkgver}
    for dir in include lib sasldb plugins utils; do
        pushd ${dir}
        make DESTDIR="${pkgdir}" install
        popd
    done
    rm -f "${pkgdir}"/usr/lib/sasl2/libsql.so*
    rm -f "${pkgdir}"/usr/lib/sasl2/libgssapiv2.so*
    rm -f "${pkgdir}"/usr/lib/sasl2/libldapdb.so*
    rm -f "${pkgdir}"/usr/lib/sasl2/libgs2.so*
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_cyrus-sasl_p() {
    depends=("libsasl=${pkgver}" 'krb5')
    pkgdesc="Cyrus saslauthd SASL authentication daemon"
    backup=('etc/conf.d/saslauthd')
    provides=("cyrus-sasl=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('cyrus-sasl' 'cyrus-sasl-plugins')
    replaces=('cyrus-sasl-plugins')

    cd ${_srcname}-${pkgver}/saslauthd
    make DESTDIR="${pkgdir}" install
    install -Dm644 "${srcdir}/saslauthd.conf.d" "${pkgdir}/etc/conf.d/saslauthd"
    install -Dm644 "${srcdir}/saslauthd.service" "${pkgdir}/usr/lib/systemd/system/saslauthd.service"
    install -Dm644 "${srcdir}/tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/saslauthd.conf"

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_cyrus-sasl-gssapi_p() {
    pkgdesc="GSSAPI authentication mechanism for Cyrus SASL"
    depends=("libsasl=${pkgver}" 'krb5')
    provides=("cyrus-sasl-gssapi=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('cyrus-sasl-gssapi' 'cyrus-sasl-plugins')
    replaces=('cyrus-sasl-plugins')

    cd ${_srcname}-${pkgver}/plugins
    install -m755 -d "${pkgdir}/usr/lib/sasl2"
    cp -a .libs/libgssapiv2.so* "${pkgdir}/usr/lib/sasl2/"
    cp -a .libs/libgs2.so* "${pkgdir}/usr/lib/sasl2/"

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_cyrus-sasl-ldap_p() {
    pkgdesc="ldapdb auxprop module for Cyrus SASL"
    depends=("libsasl=${pkgver}" 'libldap')
    provides=("cyrus-sasl-ldap=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('cyrus-sasl-ldap' 'cyrus-sasl-plugins')
    replaces=('cyrus-sasl-plugins')

    cd ${_srcname}-${pkgver}/plugins
    install -m755 -d "${pkgdir}/usr/lib/sasl2"
    cp -a .libs/libldapdb.so* "${pkgdir}/usr/lib/sasl2/"

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_cyrus-sasl-sql_p() {
    pkgdesc="SQL auxprop module for Cyrus SASL"
    depends=("libsasl=${pkgver}" 'postgresql-libs' 'libmariadbclient' 'sqlite')
    conflicts=('cyrus-sasl-sql' 'cyrus-sasl-plugins')
    replaces=('cyrus-sasl-plugins')

    cd ${_srcname}-${pkgver}/plugins
    install -m755 -d "${pkgdir}/usr/lib/sasl2"
    cp -a .libs/libsql.so* "${pkgdir}/usr/lib/sasl2/"

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
