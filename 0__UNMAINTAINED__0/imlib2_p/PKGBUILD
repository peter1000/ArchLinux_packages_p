# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

_srcname=imlib2
pkgname=${_srcname}_p
pkgver=1.4.7
pkgrel=1
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg-turbo')
provides=("imlib2=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('imlib2')
source=(http://downloads.sourceforge.net/enlightenment/$_srcname-$pkgver.tar.bz2)
sha1sums=('0ed225afbbee8e990c655cf13ed802bda8a5cd8a')

prepare() {
    cd "${_srcname}-${pkgver}"
    sed -i 's/@my_libs@//' imlib2-config.in
}

build() {
    cd "${_srcname}-${pkgver}"
    EXTRAOPTS="--enable-amd64"

    ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib $EXTRAOPTS
    make ${MAKEFLAGS}
}

package() {
    cd "${_srcname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Install License
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
