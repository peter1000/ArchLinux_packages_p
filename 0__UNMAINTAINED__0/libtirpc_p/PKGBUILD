# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_srcname=libtirpc
pkgname=${_srcname}_p
pkgver=0.3.2
pkgrel=2
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('krb5')
backup=('etc/netconfig')
provides=("libtirpc=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libtirpc')
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${_srcname}-${pkgver}.tar.bz2
        fix_missing_rpc_get_default_domain.patch)
sha1sums=('af9b74d0c4d1499a7b1a43e396e5b7d62180ea65'
          'ab3b6a2e0beb255a82dcbf02c915076b8fe43e5a')

prepare() {
    cd ${_srcname}-${pkgver}
    patch -Np1 -i ${srcdir}/fix_missing_rpc_get_default_domain.patch
}

build() {
    cd ${_srcname}-${pkgver}
    ./configure --prefix=/usr --sysconf=/etc
    make ${MAKEFLAGS}
}

package() {
    cd ${_srcname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
