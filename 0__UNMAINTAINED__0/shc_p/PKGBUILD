# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: onefire <onefire.myself@gmail.com>
# Contributor: Ewoud Nuyts <ewoud.nuyts@gmail.com>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>

_srcname=shc
pkgname=${_srcname}_p
pkgver=3.8.9
pkgrel=2
pkgdesc="Converts shell script to C source code, and then compiles it."
arch=('x86_64')
url="http://www.datsi.fi.upm.es/~frosal/sources/shc.html"
license=('GPL')
provides=("shc=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('shc')
source=("http://www.datsi.fi.upm.es/~frosal/sources/shc-${pkgver}.tgz")
sha256sums=('60f3220db9440322e8eddeea82501035931193e8570983e73b0519b6317ffe4a')

build() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    #makefile assumes this symbolic link exists in version 3.8.9
    if [ ! -f "${_srcname}".c  ]
    then
        msg "Created symbolic link ${srcdir}/${_srcname}-${pkgver}/${_srcname}.c"
        ln -s "${_srcname}"-"${pkgver}".c "${_srcname}".c
    fi
    make ${MAKEFLAGS} "${_srcname}"
}

package() {
    cd "${srcdir}/${_srcname}-${pkgver}"
    install -D -m 755 "${_srcname}" "${pkgdir}"/usr/bin/shc
    #install the man page
    gzip "${_srcname}".1
    install -D -m 644 "${_srcname}".1.gz "$pkgdir"/usr/share/man/man1/"${_srcname}".1.gz
}
