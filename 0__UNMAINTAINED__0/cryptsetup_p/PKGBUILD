# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

_srcname=cryptsetup
pkgname=${_srcname}_p
pkgver=1.6.8
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using dm-crypt"
arch=('x86_64')
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'libutil-linux')
makedepends=('util-linux')
options=('!emptydirs')
provides=("cryptsetup=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('cryptsetup')
source=(https://www.kernel.org/pub/linux/utils/cryptsetup/v1.6/${_srcname}-${pkgver}.tar.xz
        encrypt_hook
        encrypt_install
        sd-encrypt)
sha256sums=('45a6ccd3c65b7d904e58e1cb3656a7e997190b6a05b5ff7c6887e4a41c5f19bc'
            '4406f8dc83f4f1b408e49d557515f721d91b358355c71fbe51f74ab27e5c84ff'
            'cfe465bdad3d958bb2332a05e04f2e1e884422a5714dfd1a0a3b9b74bf7dc6ae'
            'd442304e6a78b3513ebc53be3fe2f1276a7df470c8da701b3ece971d59979bdd')

build() {
    cd "${srcdir}"/$_srcname-${pkgver}
    ./configure --prefix=/usr --sbindir=/usr/bin --disable-static --enable-cryptsetup-reencrypt
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}"/$_srcname-${pkgver}
    make DESTDIR="${pkgdir}" install
    # install hook
    install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
    install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
    install -D -m644 "${srcdir}"/sd-encrypt "${pkgdir}"/usr/lib/initcpio/install/sd-encrypt
}
