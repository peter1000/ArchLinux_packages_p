# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

_srcname=libgit2
pkgname=${_srcname}-git_p
pkgver=0.23.0.r316.g1318ec9         
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=('x86_64')
url='https://libgit2.github.com/'
license=('GPL2')
depends=('curl')
makedepends=('cmake' 'ninja' 'python')
provides=("libgit2=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libgit2' 'libgit2-git')
source=("${_srcname}::git+https://github.com/libgit2/${_srcname}.git#commit=1318ec9") #commit=1318ec9  OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    export LANG=en_US.UTF-8
    cmake -G Ninja -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
    ninja ${MAKEFLAGS}
}

check() {
    cd "${srcdir}/${_srcname}"
    ninja ${MAKEFLAGS} test
}

package() {
    cd "${srcdir}/${_srcname}"
    DESTDIR="$pkgdir" ninja install
}
