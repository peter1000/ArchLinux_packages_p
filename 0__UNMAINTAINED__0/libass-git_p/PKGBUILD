# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

_srcname=libass
pkgname=${_srcname}-git_p
pkgver=0.13.0.r8.g43b1520           #commit=43b1520  OK
pkgrel=1
pkgdesc="A portable library for SSA/ASS subtitles rendering"
arch=('x86_64')
url="https://github.com/libass/libass/"
license=('BSD')
depends=('fribidi' 'fontconfig' 'enca')
makedepends=('pkg-config' 'yasm')
conflicts=('libass' 'libass-git')
provides=("libass=${pkgver}" "${pkgname}=${pkgver}")
source=("${_srcname}::git+https://github.com/libass/libass.git#commit=43b1520")
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr \
              --enable-enca \
              --enable-harfbuzz \
              --enable-fontconfig
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"

    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
