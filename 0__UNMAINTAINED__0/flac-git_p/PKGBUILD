# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=flac
pkgname=${_srcname}-git_p
pkgver=1.3.1.r95.g20d721e           
pkgrel=1
arch=('x86_64')
pkgdesc="Free Lossless Audio Codec"
url="https://xiph.org/flac/"
license=('GPL' 'BSD')
depends=('gcc-libs' 'libogg')
makedepends=('git' 'nasm')
options=('!makeflags')
provides=("flac=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('flac' 'flac-git')
source=("${_srcname}::git+https://git.xiph.org/${_srcname}.git#commit=20d721e") 			#commit=20d721e  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    ./autogen.sh
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --prefix=/usr \
        --enable-sse \
        --disable-thorough-tests \
        --disable-doxygen-docs \
        --disable-oggtest \
        --disable-rpath
    make ${MAKEFLAGS}
}

check() {
    # Takes long: using --disable-thorough-tests
    cd "${srcdir}/${_srcname}"
    make check
}

package() {
    cd "${srcdir}/${_srcname}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
