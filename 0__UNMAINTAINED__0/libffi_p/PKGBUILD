# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_srcname=libffi
pkgname=${_srcname}_p
pkgver=3.2.1
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='http://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
install=libffi.install
provides=("libffi=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('libffi')
source=(ftp://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz)
sha1sums=('280c265b789e041c02e5c97815793dfc283fb1e6')

build() {
    cd $_srcname-$pkgver
    ./configure --prefix=/usr --disable-static \
        --enable-pax_emutramp
    make ${MAKEFLAGS}
}

check() {
    make ${MAKEFLAGS} -C $_srcname-$pkgver check
}

package() {
    cd $_srcname-$pkgver
    make DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
