# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor:  Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

_srcname=pacman
pkgname=${_srcname}_p
pkgver=4.2.1
pkgrel=4
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash' 'glibc' 'libarchive>=3.1.2' 'curl>=7.39.0'
         'gpgme' 'pacman-mirrorlist_p' 'archlinux-keyring_p')
makedepends=('asciidoc')   # roundup patch alters docs
checkdepends=('python2' 'fakechroot')
provides=('pacman-contrib' "pacman=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('pacman-contrib' 'pacman')
replaces=('pacman-contrib')
backup=(etc/pacman.conf etc/makepkg.conf)
options=('strip' 'debug')
source=(https://sources.archlinux.org/other/pacman/$_srcname-$pkgver.tar.gz
        ensure-matching-database-and-package-version.patch
        pacman.conf.i686
        pacman.conf.x86_64
        makepkg.conf)
sha256sums=('a142593c409ef21f6fde0075c7649a58a8ee0edbd2af3f82e4dc05cfc1d0b545'
            'c4510fa2fdc68379ecb5e75795f232b37695b1d7eb72444476a16f8be48ea313'
            '942524db1772c08daf8b39fc89366132f9404eb0718d41d827c84662ac8df76d'
            '6f11575cc4a00e2f4ba3a1ed6c7363fe92345cf6382a61e8d8021528458fe8d5'
            'a9f1d448ca8876ec6efc92c4f40ff384ad000c827914ad3a841acf5d2af50b28')

prepare() {
    cd "$_srcname-$pkgver"

    patch -p1 -i $srcdir/ensure-matching-database-and-package-version.patch
}

build() {
    cd "$_srcname-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --enable-doc \
        --with-scriptlet-shell=/usr/bin/bash \
        --with-ldconfig=/usr/bin/ldconfig
    make ${MAKEFLAGS}
    make ${MAKEFLAGS} -C contrib
}

check() {
    make -C "$_srcname-$pkgver" check
}

package() {
    cd "$_srcname-$pkgver"

    make DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" -C contrib install

    # install Arch specific stuff
    install -dm755 "$pkgdir/etc"
    install -m644 "$srcdir/pacman.conf.$CARCH" "$pkgdir/etc/pacman.conf"

    mycarch="x86_64"
    mychost="x86_64-unknown-linux-gnu"
    myflags="-march=x86-64"

    # set things correctly in the default conf file
    install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
    sed -i "$pkgdir/etc/makepkg.conf" \
        -e "s|@CARCH[@]|$mycarch|g" \
        -e "s|@CHOST[@]|$mychost|g" \
        -e "s|@CARCHFLAGS[@]|$myflags|g"

    # put bash_completion in the right location
    install -dm755 "$pkgdir/usr/share/bash-completion/completions"
    mv "$pkgdir/etc/bash_completion.d/pacman" "$pkgdir/usr/share/bash-completion/completions"
    rmdir "$pkgdir/etc/bash_completion.d"

    for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
    done

    install -Dm644 contrib/PKGBUILD.vim "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
}
