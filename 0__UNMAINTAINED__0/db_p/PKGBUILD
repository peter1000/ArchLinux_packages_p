# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

_srcname=db
pkgname=${_srcname}_p
pkgver=5.3.28
pkgrel=2
pkgdesc="The Berkeley DB embedded database system"
arch=('x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
depends=('gcc-libs' 'sh')
provides=("db=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('db')
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
sha1sums=('fa3f8a41ad5101f43d08bc0efb6241c9b6fc1ae9')

build() {
    cd "${srcdir}"/$_srcname-${pkgver}/build_unix
    ../dist/configure --prefix=/usr --enable-compat185 \
        --enable-shared --enable-static --enable-cxx --enable-dbm \
        --enable-stl
    make ${MAKEFLAGS} LIBSO_LIBS=-lpthread
}

package() {
    cd "${srcdir}"/$_srcname-${pkgver}/build_unix
    make DESTDIR="${pkgdir}" install
    rm -r "${pkgdir}"/usr/docs
    install -Dm644 "${srcdir}"/${_srcname}-${pkgver}/LICENSE \
        "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
