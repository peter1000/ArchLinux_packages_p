# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_srcname=soundtouch
pkgname=${_srcname}_p
pkgver=1.9.2
pkgrel=1
pkgdesc="An audio processing library"
arch=('x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
provides=("soundtouch=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('soundtouch')
source=("http://www.surina.net/soundtouch/${_srcname}-${pkgver}.tar.gz")
sha256sums=('caeb86511e81420eeb454cb5db53f56d96b8451d37d89af6e55b12eb4da1c513')

prepare() {
    cd "${srcdir}/${_srcname}"
    sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
}

build() {
    cd "${srcdir}/${_srcname}"
    [ "${CARCH}" = "x86_64" ] && CONFIGFLAG="--with-pic"
    ./bootstrap
    ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}"
    make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/${_srcname} install
}
