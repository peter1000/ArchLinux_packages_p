# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Darexon <cren331 aT gmailDOTcom>

_srcname=sox
pkgname=${_srcname}-git_p
pkgver=14.4.2.r5.g7e74b25               
pkgrel=1
pkgdesc="The Swiss Army knife of sound processing tools (Development version with all optional libraries)"
arch=('x86_64')
url="http://sox.sourceforge.net/Main/HomePage"
license=('GPL' 'LGPL')
depends=('libpng' 'file' 'ladspa' 'libltdl' 'libsndfile' 'libao' 'gsm' 'opencore-amr' 'lame'
    'opusfile' 'twolame' 'libmad' 'libid3tag' 'wavpack' 'libpulse')
makedepends=('git')
optdepends=('alsa-lib: for alsa plugin')
checkdepends=('time')
provides=("sox=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('sox' 'sox-git')
source=("${_srcname}::git://git.code.sf.net/p/sox/code.git#commit=7e74b25")		#commit=7e74b25  OK
#source=("${_srcname}::git+http://bogomips.org/mirrors/sox.git")   # mirror
sha256sums=('SKIP')

pkgver() {
    cur_prefix='sox-'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"
    autoreconf -fiv
}

build() {
    cd "${srcdir}/${_srcname}"
    ./configure --with-distro='Arch Linux' --prefix=/usr
    make ${MAKEFLAGS}
}

check() {
    cd "${srcdir}/${_srcname}"
    make bindir=. installcheck
}

package() {
    cd "$_srcname"
    make DESTDIR="$pkgdir/" install
    echo -e "\e[0;31mNOTE: You will need to recompile to get plugin support from optional
    dependencies if they weren't already installed when you compiled.\e[0m"
}
