# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

_srcname=vala
pkgname=${_srcname}_p
pkgver=0.30.0
pkgrel=1
pkgdesc="Compiler for the GObject type system"
arch=('x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
provides=("vala=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('vala')
source=(http://ftp.gnome.org/pub/gnome/sources/$_srcname/${pkgver:0:4}/$_srcname-$pkgver.tar.xz)
sha256sums=('61f0337b000f7ed6ef8c1fea87e0047d9bd7c0f91dd9c5b4eb70fd3fb883dedf')

build() {
    cd $_srcname-$pkgver
    ./configure --prefix=/usr --enable-vapigen
    make ${MAKEFLAGS}
}

check() {
    cd $_srcname-$pkgver
    make ${MAKEFLAGS} check
}

package() {
    cd $_srcname-$pkgver
    make DESTDIR="$pkgdir" install
}
