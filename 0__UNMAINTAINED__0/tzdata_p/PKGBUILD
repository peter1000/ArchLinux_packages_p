# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

_srcname=tzdata
pkgname=${_srcname}_p
pkgver=2015g
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('any')
url="http://www.iana.org/time-zones"
license=('GPL')
options=('!emptydirs')
provides=("tzdata=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('tzdata')
source=(http://www.iana.org/time-zones/repository/releases/${_srcname}${pkgver}.tar.gz)
sha512sums=('ad7de0e3e8753861d260f0fcbcbca327516a4dc826f7659370a7fb979e6154f1a26b23d2609acf6e1790c8c59da0b76c42f4a94bf361409c1460f6a8ffa6f879')

timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'pacificnew' 'etcetera' 'backward'
           'systemv' 'factory')

package() {
    cd ${srcdir}
    zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo ${timezones[@]}
    zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/posix ${timezones[@]}
    zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}

    zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo -p America/New_York
    install -m444 -t ${pkgdir}/usr/share/zoneinfo iso3166.tab zone1970.tab zone.tab # zone.tab is depricated and will go soon
}
