# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: jiribb <jiribb@gmail.com>
# Contributor: David Spicer <azleifel at googlemail dot com>
# Contributor: Andrew Brouwers
# Contributor: ponsfoot @ AUR
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: David Spicer <azleifel at gmail dot com>

# NOTE: Ignore the message that "lipo" and "xcodebuild" are not found; they are for Mac OS X builds only.

_srcname=HandBrake
pkgbase=handbrake-git_p
pkgname=('handbrake-cli-git_p' 'handbrake-gtk-git_p')
pkgver=0.10.0.r1065.g0057924
pkgrel=1
pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (uses mostly system libs)"
arch=('x86_64')
url="http://handbrake.fr/"
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gst-plugins-base-libs' 'gtk3' 'jansson'
    'libsamplerate'  'libnotify' 'libdvdnav' 'librsvg' 'libgudev' 'ffmpeg'
    'gst-plugins-base' 'gst-plugins-bad' 'gst-plugins-good' 'gst-plugins-ugly' 'gst-libav'
    # 'libfdk-aac'
        # NOTE: Compiles with the libfdk-aac but got notification when using:
        #       --enable-fdk-aac --disable-libav-aac
        #       $ ghb
        #       > Preset Normal: Invalid audio encoder (aac)
        #
        # so we disabled it
    )
makedepends=('cmake' 'intltool' 'python2' 'gettext' 'subversion' 'yasm')
options=('!makeflags')
source=("${_srcname}::git+https://github.com/HandBrake/${_srcname}.git"  #commit=0057924  OK
    'remove_audio_track_associations_commit_7554941.patch')
sha512sums=('SKIP'
            '8543acf2863d837039fb48dd8ebb44a39a51d449ed51fa57daa2208253e9b278eb28e1f68525f577e77a2cccb26f917ee8026e8c68d9f370d927fa1e5fea1e2c')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_srcname}"

    # PATCH : might break it: peter1000 20151125
    patch -Np1 -i "${srcdir}/remove_audio_track_associations_commit_7554941.patch"

    # python2 substitutions
    sed -i -e '1c#! /usr/bin/python2' "gtk/src/makedeps.py"
    sed -i -e '1c#! /usr/bin/python2' "make/configure.py"

    # Use more system libs
    # x265:  Gave an error so we use the bundled one maybe check this at a later time again
    # libiconv: keep this too
    for module in autoconf automake bzip2 cmake fdk-aac ffmpeg fontconfig freetype fribidi jansson lame libass \
        libbluray libdvdnav libdvdread libgnurx libmfx libogg libsamplerate libtheora libtool \
        libvorbis libvpx libxml2 m4 pkgconfig pthreadw32 x264 yasm zlib;  do
        sed -i -e "/MODULES += contrib\/$module/d" make/include/main.defs
    done
}

build() {
    cd "${srcdir}/${_srcname}"

    # --disable-fdk-aac:  Compiles with the libfdk-aac but got notification when using:
    #       --enable-fdk-aac --disable-libav-aac
    #       $ ghb
    #       > Preset Normal: Invalid audio encoder (aac)

    ./configure --build="build" \
    --prefix=/usr \
    --force \
    --verbose \
    --disable-gtk-update-checks \
    --launch-jobs=0 \
    --fetch=curl \
    --disable-x265 \
    \
    --disable-fdk-aac \
    --enable-libav-aac \
    \
    --enable-local-yasm \
    --enable-local-autotools \
    --enable-local-cmake

    install -d -m755 "build/build_tmp"

    make ${MAKEFLAGS} -C "build" TMPDIR="$srcdir/$_srcname/build/build_tmp"
}

package_handbrake-cli-git_p() {
    pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (CLI version)"
    depends=('jansson' 'libdvdnav' 'ffmpeg')
    provides=("handbrake-cli=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('handbrake-cli' 'handbrake-cli-svn' 'handbrake-gtk2')

    install -D -m755 "$srcdir/${_srcname}/build/HandBrakeCLI" "$pkgdir/usr/bin/HandBrakeCLI"
}

package_handbrake-gtk-git_p() {
    pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (GUI version)"
    depends=('libdvdnav' 'dbus-glib' 'desktop-file-utils' 'gst-plugins-base-libs' 'ffmpeg' 'jansson' 'gtk3'
        'libnotify')
    provides=("handbrake=${pkgver}" "${pkgname}=${pkgver}")
    conflicts=('handbrake' 'handbrake-svn' 'handbrake-gtk2')
    install=handbrake-gtk.install

    cd "${srcdir}/${_srcname}"
    make -C "build" DESTDIR="$pkgdir/" install

    rm -f "$pkgdir/usr/bin/HandBrakeCLI"

    # icon-theme.cache will be rebuilt on install
    rm -f "$pkgdir/usr/share/icons/hicolor/icon-theme.cache"
}
