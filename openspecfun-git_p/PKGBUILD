# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: kusakata <shohei atmark kusakata period com>

_srcname=openspecfun
pkgname=${_srcname}-git_p
pkgver=0.4.r15.g8694878               
pkgrel=1
pkgdesc="A collection of special mathematical functions"
arch=('x86_64')
url="https://github.com/JuliaLang/openspecfun"
license=('custom:Faddeeva' 'custom:Amos')
depends=('gcc-libs')
makedepends=('gcc-fortran')
provides=("openspecfun=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('openspecfun' 'openspecfun-git')
source=("${_srcname}::git+https://github.com/JuliaLang/${_srcname}.git")    #commit=8694878  OK
sha256sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/${_srcname}"
	make ${MAKEFLAGS} USEGCC=1 ARCH=${CARCH} CFLAGS="$CFLAGS -std=c99" prefix=/usr bindir=/usr/bin libdir=/usr/lib includedir=/usr/include
}

package() {
	cd "${srcdir}/${_srcname}"
	make DESTDIR="$pkgdir" prefix=/usr bindir=/usr/bin libdir=/usr/lib includedir=/usr/include install
	install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
