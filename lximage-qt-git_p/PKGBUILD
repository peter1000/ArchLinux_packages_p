# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_srcname=lximage-qt
pkgname=${_srcname}-git_p
pkgver=0.4.0.r21.g79ea1a0
pkgrel=1
pkgdesc="The LXQt image viewer"
arch=('x86_64')
url="http://lxqt.org"
license=("GPL2")
depends=('pcmanfm-qt-git_p')
provides=("lximage-qt=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('lximage-qt' 'lximage-qt-git')
install="lximage-qt.install"
source=("${_srcname}::git+https://github.com/lxde/${_srcname}.git")      #commit=79ea1a0  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    mkdir build
    cd build
    cmake -G Ninja "${srcdir}/${_srcname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    ninja ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/build"
    DESTDIR="$pkgdir" ninja install
}
