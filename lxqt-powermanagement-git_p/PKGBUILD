# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_srcname=lxqt-powermanagement
pkgname=${_srcname}-git_p
pkgver=0.10.0.r3.ge1516e6
pkgrel=1
pkgdesc="LXQt power management daemon"
arch=('x86_64')
url="http://lxqt.org"
license=("GPL2")
depends=('liblxqt-git_p' "libxcb" "qt5-svg" "upower")
provides=("lxqt-powermanagement=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('lxqt-powermanagement' 'lxqt-powermanagement-git')
source=("${_srcname}::git+https://github.com/lxde/${_srcname}.git")       #commit=e1516e6    OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    mkdir build
    cd build
    cmake -G Ninja "${srcdir}/${_srcname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    ninja ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/build"
    DESTDIR="$pkgdir" ninja install
}
