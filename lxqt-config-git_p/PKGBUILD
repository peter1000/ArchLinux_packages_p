# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_srcname=lxqt-config
pkgname=${_srcname}-git_p
pkgver=0.10.0.r8.g4f0f42e
pkgrel=1
pkgdesc="LXQt system configuration."
arch=('x86_64')
url="http://lxqt.org"
license=("GPL2")
depends=('liblxqt-git_p' "libxcursor" "qt5-svg" 'libkscreen')
provides=("lxqt-config=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('lxqt-config' 'lxqt-config-git')
source=("${_srcname}::git+https://github.com/lxde/${_srcname}.git")  #commit=4f0f42e  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix=''
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    mkdir build
    cd build
    cmake -G Ninja "${srcdir}/${_srcname}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    ninja ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/${_srcname}/build"
    DESTDIR="$pkgdir" ninja install
}
