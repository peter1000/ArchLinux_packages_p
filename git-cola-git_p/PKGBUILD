# Maintainer: peter1000 <https://github.com/peter1000>
# Contributor: Paul Weingardt <paulweingardt@web.de>
# Contributor: TDY <tdy@gmx.com>

_srcname=git-cola
pkgname=${_srcname}-git_p
pkgver=2.4.r69.g2912883
pkgrel=1
pkgdesc="A powerful GUI for Git"
url="http://git-cola.github.com/"
arch=('x86_64')
license=('GPL')
depends=('git' 'python-pyqt4>=4.4' 'icu' 'python-sip')   # namcap? still leave the dependencies: 'git' 'python-pyqt4>=4.4' 'icu' 'python-sip'
makedepends=('asciidoc' 'docbook-xsl' 'rsync' 'xmlto'  'python-jinja' 'python-sphinx>=1.1.3' 'python-pyinotify' 'tk')
options=('!makeflags')
provides=("git-cola=${pkgver}" "${pkgname}=${pkgver}")
conflicts=('git-cola' 'git-cola-git')
install=${pkgname}.install
source=("${_srcname}::git+https://github.com/davvid/${_srcname}.git")    #commit=2912883  OK
sha512sums=('SKIP')

pkgver() {
    cur_prefix='v'
    cd "${srcdir}/${_srcname}"
    local _tag_stripped="$(git describe --abbrev=0 --tags | sed 's/^'${cur_prefix}'//;s/-/./g')"
    local _rev="$(($(git rev-list --count HEAD)-$(git rev-list --count $(git describe --abbrev=0 --tags))))"
    printf "${_tag_stripped}.r${_rev}.g%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcname}"
    make ${MAKEFLAGS} all doc html
}

package() {
    cd "${srcdir}/${_srcname}"
    make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
